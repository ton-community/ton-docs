# –ö—É–ª–∏–Ω–∞—Ä–Ω–∞—è –∫–Ω–∏–≥–∞ FunC

:::warning
–≠—Ç–∞ —Å—Ç—Ä–∞–Ω–∏—Ü–∞ –ø–µ—Ä–µ–≤–µ–¥–µ–Ω–∞ —Å–æ–æ–±—â–µ—Å—Ç–≤–æ–º –Ω–∞ —Ä—É—Å—Å–∫–∏–π —è–∑—ã–∫, –Ω–æ –Ω—É–∂–¥–∞–µ—Ç—Å—è –≤ —É–ª—É—á—à–µ–Ω–∏—è—Ö. –ï—Å–ª–∏ –≤—ã —Ö–æ—Ç–∏—Ç–µ –ø—Ä–∏–Ω—è—Ç—å —É—á–∞—Å—Ç–∏–µ –≤ –ø–µ—Ä–µ–≤–æ–¥–µ —Å–≤—è–∂–∏—Ç–µ—Å—å —Å [@alexgton](https://t.me/alexgton).
:::

–û—Å–Ω–æ–≤–Ω–∞—è —Ü–µ–ª—å —Å–æ–∑–¥–∞–Ω–∏—è –∫—É–ª–∏–Ω–∞—Ä–Ω–æ–π –∫–Ω–∏–≥–∏ FunC - —Å–æ–±—Ä–∞—Ç—å –≤–µ—Å—å –æ–ø—ã—Ç —Ä–∞–∑—Ä–∞–±–æ—Ç—á–∏–∫–æ–≤ FunC –≤ –æ–¥–Ω–æ–º –º–µ—Å—Ç–µ, —á—Ç–æ–±—ã –±—É–¥—É—â–∏–µ —Ä–∞–∑—Ä–∞–±–æ—Ç—á–∏–∫–∏ –º–æ–≥–ª–∏ –∏–º –≤–æ—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å—Å—è!

–ü–æ —Å—Ä–∞–≤–Ω–µ–Ω–∏—é —Å [–¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–µ–π FunC](/v3/documentation/smart-contracts/func/docs/types), —ç—Ç–∞ —Å—Ç–∞—Ç—å—è –±–æ–ª—å—à–µ —Å–æ—Å—Ä–µ–¥–æ—Ç–æ—á–µ–Ω–∞ –Ω–∞ –ø–æ–≤—Å–µ–¥–Ω–µ–≤–Ω—ã—Ö –∑–∞–¥–∞—á–∞—Ö –∫–∞–∂–¥–æ–≥–æ —Ä–∞–∑—Ä–∞–±–æ—Ç—á–∏–∫–∞ FunC, –∫–æ—Ç–æ—Ä—ã–µ –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ —Ä–µ—à–∞—Ç—å –ø—Ä–∏ —Ä–∞–∑—Ä–∞–±–æ—Ç–∫–µ —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç–æ–≤.

## –û—Å–Ω–æ–≤—ã

### –ö–∞–∫ –Ω–∞–ø–∏—Å–∞—Ç—å –æ–ø–µ—Ä–∞—Ç–æ—Ä if

–î–æ–ø—É—Å—Ç–∏–º, –º—ã —Ö–æ—Ç–∏–º –ø—Ä–æ–≤–µ—Ä–∏—Ç—å, –∏–º–µ–µ—Ç –ª–∏ –∑–Ω–∞—á–µ–Ω–∏–µ –∫–∞–∫–æ–µ-–ª–∏–±–æ —Å–æ–±—ã—Ç–∏–µ. –î–ª—è —ç—Ç–æ–≥–æ –º—ã –∏—Å–ø–æ–ª—å–∑—É–µ–º –ø–µ—Ä–µ–º–µ–Ω–Ω—É—é —Ñ–ª–∞–≥–∞. –ü–æ–º–Ω–∏—Ç–µ, —á—Ç–æ –≤ FunC `true` ‚Äî —ç—Ç–æ `-1`, –∞ `false` ‚Äî —ç—Ç–æ `0`.

```func
int flag = 0; ;; false

if (flag) { 
    ;; do something
}
else {
    ;; reject the transaction
}
```

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –ù–∞–º –Ω–µ –Ω—É–∂–µ–Ω –æ–ø–µ—Ä–∞—Ç–æ—Ä `==`, –ø–æ—Ç–æ–º—É —á—Ç–æ –∑–Ω–∞—á–µ–Ω–∏–µ `0` ‚Äî —ç—Ç–æ `false`, –ø–æ—ç—Ç–æ–º—É –ª—é–±–æ–µ –¥—Ä—É–≥–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ –±—É–¥–µ—Ç `true`.

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–û–ø–µ—Ä–∞—Ç–æ—Ä If" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/statements#if-statements)

### –ö–∞–∫ –Ω–∞–ø–∏—Å–∞—Ç—å —Ü–∏–∫–ª repeat

–ö–∞–∫ –ø—Ä–∏–º–µ—Ä, –≤–æ–∑—å–º–µ–º –≤–æ–∑–≤–µ–¥–µ–Ω–∏–µ –≤ —Å—Ç–µ–ø–µ–Ω—å

```func
int number = 2;
int multiplier = number;
int degree = 5;

repeat(degree - 1) {

    number *= multiplier;
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–¶–∏–∫–ª repeat" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/statements#repeat-loop)

### –ö–∞–∫ –Ω–∞–ø–∏—Å–∞—Ç—å —Ü–∏–∫–ª while

While –ø–æ–ª–µ–∑–µ–Ω, –∫–æ–≥–¥–∞ –º—ã –Ω–µ –∑–Ω–∞–µ–º, –∫–∞–∫ —á–∞—Å—Ç–æ –≤—ã–ø–æ–ª–Ω—è—Ç—å –æ–ø—Ä–µ–¥–µ–ª–µ–Ω–Ω–æ–µ –¥–µ–π—Å—Ç–≤–∏–µ. –ù–∞–ø—Ä–∏–º–µ—Ä, –≤–æ–∑—å–º–µ–º `cell`, –∫–æ—Ç–æ—Ä–∞—è, –∫–∞–∫ –∏–∑–≤–µ—Å—Ç–Ω–æ, —Ö—Ä–∞–Ω–∏—Ç –¥–æ —á–µ—Ç—ã—Ä–µ—Ö —Å—Å—ã–ª–æ–∫ –Ω–∞ –¥—Ä—É–≥–∏–µ —è—á–µ–π–∫–∏.

```func
cell inner_cell = begin_cell() ;; create a new empty builder
        .store_uint(123, 16) ;; store uint with value 123 and length 16 bits
        .end_cell(); ;; convert builder to a cell

cell message = begin_cell()
        .store_ref(inner_cell) ;; store cell as reference
        .store_ref(inner_cell)
        .end_cell();

slice msg = message.begin_parse(); ;; convert cell to slice
while (msg.slice_refs_empty?() != -1) { ;; we should remind that -1 is true
    cell inner_cell = msg~load_ref(); ;; load cell from slice msg
    ;; do something
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–¶–∏–∫–ª while" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/statements#while-loop)
>
> ["Cell" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/concepts/dive-into-ton/ton-blockchain/cells-as-data-storage)
>
> ["slice_refs_empty?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#slice_refs_empty)
>
> ["store_ref()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_ref)
>
> ["begin_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_cell)
>
> ["end_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#end_cell)
>
> ["begin_parse()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_parse)

### –ö–∞–∫ –Ω–∞–ø–∏—Å–∞—Ç—å —Ü–∏–∫–ª do until

–ö–æ–≥–¥–∞ –Ω–∞–º –Ω—É–∂–Ω–æ, —á—Ç–æ–±—ã —Ü–∏–∫–ª –≤—ã–ø–æ–ª–Ω–∏–ª—Å—è —Ö–æ—Ç—è –±—ã –æ–¥–∏–Ω —Ä–∞–∑, –º—ã –∏—Å–ø–æ–ª—å–∑—É–µ–º `do until`.

```func
int flag = 0;

do {
    ;; do something even flag is false (0) 
} until (flag == -1); ;; -1 is true
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["Until loop" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/statements#until-loop)

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, –ø—É—Å—Ç –ª–∏ —Å—Ä–µ–∑

–ü–µ—Ä–µ–¥ —Ç–µ–º, –∫–∞–∫ —Ä–∞–±–æ—Ç–∞—Ç—å —Å `slice`, –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ –ø—Ä–æ–≤–µ—Ä–∏—Ç—å, –µ—Å—Ç—å –ª–∏ —É –Ω–µ–≥–æ –∫–∞–∫–∏–µ-–ª–∏–±–æ –¥–∞–Ω–Ω—ã–µ, —á—Ç–æ–±—ã –ø—Ä–∞–≤–∏–ª—å–Ω–æ –∏—Ö –æ–±—Ä–∞–±–æ—Ç–∞—Ç—å. –î–ª—è —ç—Ç–æ–≥–æ –º–æ–∂–Ω–æ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å `slice_empty?()`, –Ω–æ –Ω—É–∂–Ω–æ —É—á–∏—Ç—ã–≤–∞—Ç—å, —á—Ç–æ –æ–Ω –≤–µ—Ä–Ω–µ—Ç `0` (`false`), –µ—Å–ª–∏ –µ—Å—Ç—å —Ö–æ—Ç—è –±—ã –æ–¥–∏–Ω `bit` –¥–∞–Ω–Ω—ã—Ö –∏–ª–∏ –æ–¥–Ω–∞ `ref`.

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_empty?()` returns `true`, because slice doesn't have any `bits` and `refs`
empty_slice.slice_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_only.slice_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_empty?()` returns `false`, because slice have any `refs`
slice_with_refs_only.slice_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_empty?()` returns `false`, because slice have any `bits` and `refs`
slice_with_bits_and_refs.slice_empty?();
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["slice_empty?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#slice_empty)
>
> ["store_slice()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_slice)
>
> ["store_ref()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_ref)
>
> ["begin_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_cell)
>
> ["end_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#end_cell)
>
> ["begin_parse()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_parse)

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, —è–≤–ª—è–µ—Ç—Å—è –ª–∏ —Å—Ä–µ–∑ –ø—É—Å—Ç—ã–º (–Ω–µ –∏–º–µ–µ—Ç –±–∏—Ç–æ–≤, –Ω–æ –º–æ–∂–µ—Ç –∏–º–µ—Ç—å —Å—Å—ã–ª–∫–∏)

–ï—Å–ª–∏ –Ω–∞–º –Ω—É–∂–Ω–æ –ø—Ä–æ–≤–µ—Ä–∏—Ç—å —Ç–æ–ª—å–∫–æ `bits` –∏ –Ω–µ–≤–∞–∂–Ω–æ, –µ—Å—Ç—å –ª–∏ –∫–∞–∫–∏–µ-–ª–∏–±–æ `refs` –≤ `slice`, —Ç–æ –Ω–∞–º —Å–ª–µ–¥—É–µ—Ç –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å `slice_data_empty?()`.

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_data_empty?()` returns `true`, because slice doesn't have any `bits`
empty_slice.slice_data_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_data_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_only.slice_data_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_data_empty?()` returns `true`, because slice doesn't have any `bits`
slice_with_refs_only.slice_data_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_data_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_and_refs.slice_data_empty?();
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["slice_data_empty?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#slice_data_empty)
>
> ["store_slice()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_slice)
>
> ["store_ref()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_ref)
>
> ["begin_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_cell)
>
> ["end_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#end_cell)
>
> ["begin_parse()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_parse)

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, —è–≤–ª—è–µ—Ç—Å—è –ª–∏ —Å—Ä–µ–∑ –ø—É—Å—Ç—ã–º (–Ω–µ –∏–º–µ–µ—Ç —Å—Å—ã–ª–æ–∫, –Ω–æ –º–æ–∂–µ—Ç –∏–º–µ—Ç—å –±–∏—Ç—ã)

–í —Å–ª—É—á–∞–µ, –µ—Å–ª–∏ –Ω–∞—Å –∏–Ω—Ç–µ—Ä–µ—Å—É—é—Ç —Ç–æ–ª—å–∫–æ `refs`, –º—ã –¥–æ–ª–∂–Ω—ã –ø—Ä–æ–≤–µ—Ä–∏—Ç—å –∏—Ö –Ω–∞–ª–∏—á–∏–µ —Å –ø–æ–º–æ—â—å—é `slice_refs_empty?()`.

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_refs_empty?()` returns `true`, because slice doesn't have any `refs`
empty_slice.slice_refs_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_refs_empty?()` returns `true`, because slice doesn't have any `refs`
slice_with_bits_only.slice_refs_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_refs_empty?()` returns `false`, because slice have any `refs`
slice_with_refs_only.slice_refs_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_refs_empty?()` returns `false`, because slice have any `refs`
slice_with_bits_and_refs.slice_refs_empty?();
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["slice_refs_empty?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#slice_refs_empty)
>
> ["store_slice()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_slice)
>
> ["store_ref()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_ref)
>
> ["begin_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_cell)
>
> ["end_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#end_cell)
>
> ["begin_parse()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_parse)

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, –ø—É—Å—Ç–∞ –ª–∏ —è—á–µ–π–∫–∞

–ß—Ç–æ–±—ã –ø—Ä–æ–≤–µ—Ä–∏—Ç—å, –µ—Å—Ç—å –ª–∏ –∫–∞–∫–∏–µ-–ª–∏–±–æ –¥–∞–Ω–Ω—ã–µ –≤ `cell`, –º—ã –¥–æ–ª–∂–Ω—ã —Å–Ω–∞—á–∞–ª–∞ –ø—Ä–µ–æ–±—Ä–∞–∑–æ–≤–∞—Ç—å –µ–µ –≤ `slice`. –ï—Å–ª–∏ –Ω–∞—Å –∏–Ω—Ç–µ—Ä–µ—Å—É—é—Ç —Ç–æ–ª—å–∫–æ `bits`, –º—ã –¥–æ–ª–∂–Ω—ã –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å `slice_data_empty?()`, –µ—Å–ª–∏ —Ç–æ–ª—å–∫–æ `refs` - `slice_refs_empty?()`. –í —Å–ª—É—á–∞–µ, –µ—Å–ª–∏ –º—ã —Ö–æ—Ç–∏–º –ø—Ä–æ–≤–µ—Ä–∏—Ç—å –Ω–∞–ª–∏—á–∏–µ –∫–∞–∫–∏—Ö-–ª–∏–±–æ –¥–∞–Ω–Ω—ã—Ö, –Ω–µ–∑–∞–≤–∏—Å–∏–º–æ –æ—Ç —Ç–æ–≥–æ, —è–≤–ª—è—é—Ç—Å—è –ª–∏ –æ–Ω–∏ `bit` –∏–ª–∏ `ref`, –º—ã –¥–æ–ª–∂–Ω—ã –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å `slice_empty?()`.

```func
cell cell_with_bits_and_refs = begin_cell()
    .store_uint(1337, 16)
    .store_ref(null())
    .end_cell();

;; Change `cell` type to slice with `begin_parse()`
slice cs = cell_with_bits_and_refs.begin_parse();

;; determine if slice is empty
if (cs.slice_empty?()) {
    ;; cell is empty
}
else {
    ;; cell is not empty
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["slice_empty?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#slice_empty)
>
> ["begin_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_cell)
>
> ["store_uint()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_uint)
>
> ["end_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#end_cell)
>
> ["begin_parse()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_parse)

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, –ø—É—Å—Ç –ª–∏ —Å–ª–æ–≤–∞—Ä—å

–°—É—â–µ—Å—Ç–≤—É–µ—Ç –º–µ—Ç–æ–¥ `dict_empty?()` –¥–ª—è –ø—Ä–æ–≤–µ—Ä–∫–∏ –Ω–∞–ª–∏—á–∏—è –¥–∞–Ω–Ω—ã—Ö –≤ —Å–ª–æ–≤–∞—Ä–µ. –≠—Ç–æ—Ç –º–µ—Ç–æ–¥ —ç–∫–≤–∏–≤–∞–ª–µ–Ω—Ç–µ–Ω `cell_null?()`, –ø–æ—Å–∫–æ–ª—å–∫—É –æ–±—ã—á–Ω–æ `null`-—è—á–µ–π–∫–∞ —è–≤–ª—è–µ—Ç—Å—è –ø—É—Å—Ç—ã–º —Å–ª–æ–≤–∞—Ä–µ–º.

```func
cell d = new_dict();
d~udict_set(256, 0, "hello");
d~udict_set(256, 1, "world");

if (d.dict_empty?()) { ;; Determine if dict is empty
    ;; dict is empty
}
else {
    ;; dict is not empty
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["dict_empty?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#dict_empty)
>
> ["new_dict()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#new_dict) —Å–æ–∑–¥–∞–Ω–∏–µ –ø—É—Å—Ç–æ–≥–æ —Å–ª–æ–≤–∞—Ä—è
>
> ["dict_set()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_set) –¥–æ–±–∞–≤–ª–µ–Ω–∏–µ –Ω–µ–∫–æ—Ç–æ—Ä—ã—Ö —ç–ª–µ–º–µ–Ω—Ç–æ–≤ –≤ —Å–ª–æ–≤–∞—Ä—å d —Å –ø–æ–º–æ—â—å—é —Ñ—É–Ω–∫—Ü–∏–∏, —á—Ç–æ–±—ã –æ–Ω –Ω–µ –±—ã–ª –ø—É—Å—Ç—ã–º

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, –ø—É—Å—Ç –ª–∏ –∫–æ—Ä—Ç–µ–∂

–ü—Ä–∏ —Ä–∞–±–æ—Ç–µ —Å `tuples` –≤—Å–µ–≥–¥–∞ –≤–∞–∂–Ω–æ –∑–Ω–∞—Ç—å, –µ—Å—Ç—å –ª–∏ –≤–Ω—É—Ç—Ä–∏ –∫–∞–∫–∏–µ-–ª–∏–±–æ –∑–Ω–∞—á–µ–Ω–∏—è –¥–ª—è –∏–∑–≤–ª–µ—á–µ–Ω–∏—è. –ï—Å–ª–∏ –º—ã –ø–æ–ø—ã—Ç–∞–µ–º—Å—è –∏–∑–≤–ª–µ—á—å –∑–Ω–∞—á–µ–Ω–∏–µ –∏–∑ –ø—É—Å—Ç–æ–≥–æ `tuple`, —Ç–æ –ø–æ–ª—É—á–∏–º –æ—à–∏–±–∫—É: "–∫–æ—Ä—Ç–µ–∂ –Ω–µ–¥–æ–ø—É—Å—Ç–∏–º–æ–≥–æ —Ä–∞–∑–º–µ—Ä–∞" —Å `exit code 7`.

```func
;; Declare tlen function because it's not presented in stdlib
(int) tlen (tuple t) asm "TLEN";

() main () {
    tuple t = empty_tuple();
    t~tpush(13);
    t~tpush(37);

    if (t.tlen() == 0) {
        ;; tuple is empty
    }
    else {
        ;; tuple is not empty
    }
}
```

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –ú—ã –æ–±—ä—è–≤–ª—è–µ–º —Ñ—É–Ω–∫—Ü–∏—é —Å–±–æ—Ä–∫–∏ tlen. –í—ã –º–æ–∂–µ—Ç–µ –ø—Ä–æ—á–∏—Ç–∞—Ç—å –±–æ–ª—å—à–µ [–∑–¥–µ—Å—å](/v3/documentation/smart-contracts/func/docs/functions#assembler-function-body-definition) –∏ –ø–æ—Å–º–æ—Ç—Ä–µ—Ç—å [—Å–ø–∏—Å–æ–∫ –≤—Å–µ—Ö –∫–æ–º–∞–Ω–¥ –∞—Å—Å–µ–º–±–ª–µ—Ä–∞](/v3/documentation/tvm/instructions).

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["empty_tuple?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#empty_tuple)
>
> ["tpush()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#tpush)
>
> ["–ö–æ–¥—ã –≤—ã—Ö–æ–¥–∞" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/tvm/tvm-exit-codes)

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, –ø—É—Å—Ç –ª–∏ lisp-–ø–æ–¥–æ–±–Ω—ã–π —Å–ø–∏—Å–æ–∫

```func
tuple numbers = null();
numbers = cons(100, numbers);

if (numbers.null?()) {
    ;; list-style list is empty
} else {
    ;; list-style list is not empty
}
```

–ú—ã –¥–æ–±–∞–≤–ª—è–µ–º —á–∏—Å–ª–æ 100 –≤ –Ω–∞—à lisp-–ø–æ–¥–æ–±–Ω—ã–π —Å–ø–∏—Å–æ–∫ —Å –ø–æ–º–æ—â—å—é —Ñ—É–Ω–∫—Ü–∏–∏ [cons](/v3/documentation/smart-contracts/func/docs/stdlib/#cons), –ø–æ—ç—Ç–æ–º—É –æ–Ω –Ω–µ –ø—É—Å—Ç.

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, —á—Ç–æ —Å–æ—Å—Ç–æ—è–Ω–∏–µ –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞ –ø—É—Å—Ç–æ–µ

–î–æ–ø—É—Å—Ç–∏–º, —É –Ω–∞—Å –µ—Å—Ç—å `counter`, –∫–æ—Ç–æ—Ä—ã–π —Ö—Ä–∞–Ω–∏—Ç –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ —Ç—Ä–∞–Ω–∑–∞–∫—Ü–∏–π. –≠—Ç–∞ –ø–µ—Ä–µ–º–µ–Ω–Ω–∞—è –Ω–µ–¥–æ—Å—Ç—É–ø–Ω–∞ –≤–æ –≤—Ä–µ–º—è –ø–µ—Ä–≤–æ–π —Ç—Ä–∞–Ω–∑–∞–∫—Ü–∏–∏ –≤ —Å–æ—Å—Ç–æ—è–Ω–∏–∏ —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞, –ø–æ—Å–∫–æ–ª—å–∫—É —Å–æ—Å—Ç–æ—è–Ω–∏–µ –ø—É—Å—Ç–æ–µ, –ø–æ—ç—Ç–æ–º—É –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ –æ–±—Ä–∞–±–æ—Ç–∞—Ç—å —Ç–∞–∫–æ–π —Å–ª—É—á–∞–π. –ï—Å–ª–∏ —Å–æ—Å—Ç–æ—è–Ω–∏–µ –ø—É—Å—Ç–æ–µ, –º—ã —Å–æ–∑–¥–∞–µ–º –ø–µ—Ä–µ–º–µ–Ω–Ω—É—é `counter` –∏ —Å–æ—Ö—Ä–∞–Ω—è–µ–º –µ–µ.

```func
;; `get_data()` will return the data cell from contract state
cell contract_data = get_data();
slice cs = contract_data.begin_parse();

if (cs.slice_empty?()) {
    ;; contract data is empty, so we create counter and save it
    int counter = 1;
    ;; create cell, add counter and save in contract state
    set_data(begin_cell().store_uint(counter, 32).end_cell());
}
else {
    ;; contract data is not empty, so we get our counter, increase it and save
    ;; we should specify correct length of our counter in bits
    int counter = cs~load_uint(32) + 1;
    set_data(begin_cell().store_uint(counter, 32).end_cell());
}
```

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –ú—ã –º–æ–∂–µ–º –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å, —á—Ç–æ —Å–æ—Å—Ç–æ—è–Ω–∏–µ –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞ –ø—É—Å—Ç–æ–µ, –æ–ø—Ä–µ–¥–µ–ª–∏–≤, —á—Ç–æ [—è—á–µ–π–∫–∞ –ø—É—Å—Ç–∞](/v3/documentation/smart-contracts/func/cookbook#how-to-determine-if-cell-is-empty).

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["get_data()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#get_data)
>
> ["begin_parse()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#begin_parse)
>
> ["slice_empty?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_empty)
>
> ["set_data?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#set_data)

### –ö–∞–∫ –ø–æ—Å—Ç—Ä–æ–∏—Ç—å —è—á–µ–π–∫—É –≤–Ω—É—Ç—Ä–µ–Ω–Ω–µ–≥–æ —Å–æ–æ–±—â–µ–Ω–∏—è

–ï—Å–ª–∏ –º—ã —Ö–æ—Ç–∏–º, —á—Ç–æ–±—ã –∫–æ–Ω—Ç—Ä–∞–∫—Ç –æ—Ç–ø—Ä–∞–≤–ª—è–ª –≤–Ω—É—Ç—Ä–µ–Ω–Ω–µ–µ —Å–æ–æ–±—â–µ–Ω–∏–µ, –º—ã –¥–æ–ª–∂–Ω—ã —Å–Ω–∞—á–∞–ª–∞ –ø—Ä–∞–≤–∏–ª—å–Ω–æ —Å–æ–∑–¥–∞—Ç—å –µ–≥–æ –∫–∞–∫ —è—á–µ–π–∫—É, —É–∫–∞–∑–∞–≤ —Ç–µ—Ö–Ω–∏—á–µ—Å–∫–∏–µ —Ñ–ª–∞–≥–∏, –∞–¥—Ä–µ—Å –ø–æ–ª—É—á–∞—Ç–µ–ª—è –∏ –æ—Å—Ç–∞–ª—å–Ω—ã–µ –¥–∞–Ω–Ω—ã–µ.

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
;; we use `op` for identifying operations
int op = 0;

cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(op, 32)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –í —ç—Ç–æ–º –ø—Ä–∏–º–µ—Ä–µ –º—ã –∏—Å–ø–æ–ª—å–∑—É–µ–º –ª–∏—Ç–µ—Ä–∞–ª `a` –¥–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –∞–¥—Ä–µ—Å–∞. –ü–æ–¥—Ä–æ–±–Ω–µ–µ –æ —Å—Ç—Ä–æ–∫–æ–≤—ã—Ö –ª–∏—Ç–µ—Ä–∞–ª–∞—Ö –º–æ–∂–Ω–æ —É–∑–Ω–∞—Ç—å –≤ [–¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/literals_identifiers#string-literals)

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –ü–æ–¥—Ä–æ–±–Ω–µ–µ –º–æ–∂–Ω–æ —É–∑–Ω–∞—Ç—å –≤ [–¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/message-management/sending-messages). –¢–∞–∫–∂–µ –º–æ–∂–Ω–æ –ø–µ—Ä–µ–π—Ç–∏ –∫ [–º–∞–∫–µ—Ç—É](/v3/documentation/smart-contracts/message-management/sending-messages#message-layout) –ø–æ —ç—Ç–æ–π —Å—Å—ã–ª–∫–µ.

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["begin_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_cell)
>
> ["store_uint()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_uint)
>
> ["store_slice()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_slice)
>
> ["store_coins()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_coins)
>
> ["end_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#end_cell)
>
> ["send_raw_message()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#send_raw_message)

### –ö–∞–∫ –≤–∫–ª—é—á–∏—Ç—å —Ç–µ–ª–æ –≤ –∫–∞—á–µ—Å—Ç–≤–µ —Å—Å—ã–ª–∫–∏ –Ω–∞ –≤–Ω—É—Ç—Ä–µ–Ω–Ω—é—é —è—á–µ–π–∫—É —Å–æ–æ–±—â–µ–Ω–∏—è

–í —Ç–µ–ª–µ —Å–æ–æ–±—â–µ–Ω–∏—è, –∫–æ—Ç–æ—Ä–æ–µ —Å–ª–µ–¥—É–µ—Ç –∑–∞ —Ñ–ª–∞–≥–∞–º–∏ –∏ –¥—Ä—É–≥–∏–º–∏ —Ç–µ—Ö–Ω–∏—á–µ—Å–∫–∏–º–∏ –¥–∞–Ω–Ω—ã–º–∏, –º—ã –º–æ–∂–µ–º –æ—Ç–ø—Ä–∞–≤–∏—Ç—å `int`, `slice` –∏ `cell`. –í –ø–æ—Å–ª–µ–¥–Ω–µ–º —Å–ª—É—á–∞–µ –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –±–∏—Ç —Ä–∞–≤–Ω—ã–º `1` –ø–µ—Ä–µ–¥ `store_ref()`, —á—Ç–æ–±—ã —É–∫–∞–∑–∞—Ç—å, —á—Ç–æ `cell` –±—É–¥–µ—Ç –ø—Ä–æ–¥–æ–ª–∂–µ–Ω–∞.

–ú—ã —Ç–∞–∫–∂–µ –º–æ–∂–µ–º –æ—Ç–ø—Ä–∞–≤–∏—Ç—å —Ç–µ–ª–æ —Å–æ–æ–±—â–µ–Ω–∏—è –≤–Ω—É—Ç—Ä–∏ —Ç–æ–π –∂–µ `cell`, —á—Ç–æ –∏ –∑–∞–≥–æ–ª–æ–≤–æ–∫, –µ—Å–ª–∏ —É–≤–µ—Ä–µ–Ω—ã, —á—Ç–æ —É –Ω–∞—Å –¥–æ—Å—Ç–∞—Ç–æ—á–Ω–æ –º–µ—Å—Ç–∞. –í —ç—Ç–æ–º —Å–ª—É—á–∞–µ –Ω–∞–º –Ω—É–∂–Ω–æ —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –±–∏—Ç —Ä–∞–≤–Ω—ã–º `0`.

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
int op = 0;
cell message_body = begin_cell() ;; Creating a cell with message
    .store_uint(op, 32)
    .store_slice("‚ù§")
.end_cell();
    
cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1) ;; default message headers (see sending messages page)
    .store_uint(1, 1) ;; set bit to 1 to indicate that the cell will go on
    .store_ref(message_body)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –í —ç—Ç–æ–º –ø—Ä–∏–º–µ—Ä–µ –º—ã –∏—Å–ø–æ–ª—å–∑—É–µ–º –ª–∏—Ç–µ—Ä–∞–ª `a` –¥–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –∞–¥—Ä–µ—Å–∞. –ü–æ–¥—Ä–æ–±–Ω–µ–µ –æ —Å—Ç—Ä–æ–∫–æ–≤—ã—Ö –ª–∏—Ç–µ—Ä–∞–ª–∞—Ö –º–æ–∂–Ω–æ —É–∑–Ω–∞—Ç—å –≤ [–¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/literals_identifiers#string-literals)

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –í —ç—Ç–æ–º –ø—Ä–∏–º–µ—Ä–µ –º—ã –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–ª–∏ —Ä–µ–∂–∏–º 3, —á—Ç–æ–±—ã –≤–∑—è—Ç—å –≤—Ö–æ–¥—è—â–∏–µ ton –∏ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å —Ä–æ–≤–Ω–æ —Å—Ç–æ–ª—å–∫–æ, —Å–∫–æ–ª—å–∫–æ —É–∫–∞–∑–∞–Ω–æ (—Å—É–º–º–∞), –ø—Ä–∏ —ç—Ç–æ–º –≤—ã–ø–ª–∞—á–∏–≤–∞—è –∫–æ–º–∏—Å—Å–∏—é —Å –±–∞–ª–∞–Ω—Å–∞ –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞ –∏ –∏–≥–Ω–æ—Ä–∏—Ä—É—è –æ—à–∏–±–∫–∏. –†–µ–∂–∏–º 64 –Ω–µ–æ–±—Ö–æ–¥–∏–º –¥–ª—è –≤–æ–∑–≤—Ä–∞—Ç–∞ –≤—Å–µ—Ö –ø–æ–ª—É—á–µ–Ω–Ω—ã—Ö ton, –≤—ã—á–∏—Ç–∞—è –∫–æ–º–∏—Å—Å–∏—é, –∞ —Ä–µ–∂–∏–º 128 –æ—Ç–ø—Ä–∞–≤–∏—Ç –≤–µ—Å—å –±–∞–ª–∞–Ω—Å.

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –ú—ã [—Å–æ–∑–¥–∞–µ–º —Å–æ–æ–±—â–µ–Ω–∏–µ](/v3/documentation/smart-contracts/func/cookbook#how-to-build-an-internal-message-cell), –Ω–æ –¥–æ–±–∞–≤–ª—è–µ–º —Ç–µ–ª–æ —Å–æ–æ–±—â–µ–Ω–∏—è –æ—Ç–¥–µ–ª—å–Ω–æ.

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["begin_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#begin_cell)
>
> ["store_uint()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_uint)
>
> ["store_slice()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_slice)
>
> ["store_coins()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#store_coins)
>
> ["end_cell()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#end_cell)
>
> ["send_raw_message()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#send_raw_message)

### –ö–∞–∫ –≤–∫–ª—é—á–∏—Ç—å —Ç–µ–ª–æ –≤ –∫–∞—á–µ—Å—Ç–≤–µ —Å—Ä–µ–∑–∞ –≤–æ –≤–Ω—É—Ç—Ä–µ–Ω–Ω—é—é —è—á–µ–π–∫—É —Å–æ–æ–±—â–µ–Ω–∏—è

–ü—Ä–∏ –æ—Ç–ø—Ä–∞–≤–∫–µ —Å–æ–æ–±—â–µ–Ω–∏–π —Ç–µ–ª–æ —Å–æ–æ–±—â–µ–Ω–∏—è –º–æ–∂–µ—Ç –±—ã—Ç—å –æ—Ç–ø—Ä–∞–≤–ª–µ–Ω–æ –ª–∏–±–æ –∫–∞–∫ `cell`, –ª–∏–±–æ –∫–∞–∫ `slice`. –í —ç—Ç–æ–º –ø—Ä–∏–º–µ—Ä–µ –º—ã –æ—Ç–ø—Ä–∞–≤–ª—è–µ–º —Ç–µ–ª–æ —Å–æ–æ–±—â–µ–Ω–∏—è –≤–Ω—É—Ç—Ä–∏ `slice`.

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
int op = 0;
slice message_body = "‚ù§"; 

cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(op, 32)
    .store_slice(message_body)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –í —ç—Ç–æ–º –ø—Ä–∏–º–µ—Ä–µ –º—ã –∏—Å–ø–æ–ª—å–∑—É–µ–º –ª–∏—Ç–µ—Ä–∞–ª `a` –¥–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –∞–¥—Ä–µ—Å–∞. –ü–æ–¥—Ä–æ–±–Ω–µ–µ –æ —Å—Ç—Ä–æ–∫–æ–≤—ã—Ö –ª–∏—Ç–µ—Ä–∞–ª–∞—Ö –º–æ–∂–Ω–æ —É–∑–Ω–∞—Ç—å –≤ [–¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/literals_identifiers#string-literals)

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –í —ç—Ç–æ–º –ø—Ä–∏–º–µ—Ä–µ –º—ã –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–ª–∏ —Ä–µ–∂–∏–º 3, —á—Ç–æ–±—ã –≤–∑—è—Ç—å –≤—Ö–æ–¥—è—â–∏–µ ton –∏ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å —Ä–æ–≤–Ω–æ —Å—Ç–æ–ª—å–∫–æ, —Å–∫–æ–ª—å–∫–æ —É–∫–∞–∑–∞–Ω–æ (—Å—É–º–º–∞), –ø—Ä–∏ —ç—Ç–æ–º –≤—ã–ø–ª–∞—á–∏–≤–∞—è –∫–æ–º–∏—Å—Å–∏—é –∏–∑ –±–∞–ª–∞–Ω—Å–∞ –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞ –∏ –∏–≥–Ω–æ—Ä–∏—Ä—É—è –æ—à–∏–±–∫–∏. –†–µ–∂–∏–º 64 –Ω–µ–æ–±—Ö–æ–¥–∏–º –¥–ª—è –≤–æ–∑–≤—Ä–∞—Ç–∞ –≤—Å–µ—Ö –ø–æ–ª—É—á–µ–Ω–Ω—ã—Ö ton, –≤—ã—á–∏—Ç–∞—è –∫–æ–º–∏—Å—Å–∏—é, –∞ —Ä–µ–∂–∏–º 128 –æ—Ç–ø—Ä–∞–≤–∏—Ç –≤–µ—Å—å –±–∞–ª–∞–Ω—Å.

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –ú—ã [—Å—Ç—Ä–æ–∏–º —Å–æ–æ–±—â–µ–Ω–∏–µ](/v3/documentation/smart-contracts/func/cookbook#how-to-build-an-internal-message-cell), –Ω–æ –¥–æ–±–∞–≤–ª—è–µ–º —Å–æ–æ–±—â–µ–Ω–∏–µ –∫–∞–∫ —Å—Ä–µ–∑.

### –ö–∞–∫ –ø–µ—Ä–µ–±–∏—Ä–∞—Ç—å –∫–æ—Ä—Ç–µ–∂–∏ (–≤ –æ–±–æ–∏—Ö –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏—è—Ö)

–ï—Å–ª–∏ –º—ã —Ö–æ—Ç–∏–º —Ä–∞–±–æ—Ç–∞—Ç—å —Å –º–∞—Å—Å–∏–≤–æ–º –∏–ª–∏ —Å—Ç–µ–∫–æ–º –≤ FunC, —Ç–æ —Ç–∞–º –ø–æ–Ω–∞–¥–æ–±–∏—Ç—Å—è –∫–æ—Ä—Ç–µ–∂. –ò –≤ –ø–µ—Ä–≤—É—é –æ—á–µ—Ä–µ–¥—å –Ω–∞–º –Ω—É–∂–Ω–æ —É–º–µ—Ç—å –ø–µ—Ä–µ–±–∏—Ä–∞—Ç—å –∑–Ω–∞—á–µ–Ω–∏—è, —á—Ç–æ–±—ã —Ä–∞–±–æ—Ç–∞—Ç—å —Å –Ω–∏–º–∏.

```func
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

() main () {
    tuple t = to_tuple([1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
    int len = t.tlen();
    
    int i = 0;
    while (i < len) {
        int x = t.at(i);
        ;; do something with x
        i = i + 1;
    }

    i = len - 1;
    while (i >= 0) {
        int x = t.at(i);
        ;; do something with x
        i = i - 1;
    }
}
```

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –ú—ã –æ–±—ä—è–≤–ª—è–µ–º —Ñ—É–Ω–∫—Ü–∏—é –∞—Å—Å–µ–º–±–ª–µ—Ä–∞ `tlen`. –í—ã –º–æ–∂–µ—Ç–µ –ø—Ä–æ—á–∏—Ç–∞—Ç—å –±–æ–ª—å—à–µ [–∑–¥–µ—Å—å](/v3/documentation/smart-contracts/func/docs/functions#assembler-function-body-definition) –∏ –ø–æ—Å–º–æ—Ç—Ä–µ—Ç—å [—Å–ø–∏—Å–æ–∫ –≤—Å–µ—Ö –∫–æ–º–∞–Ω–¥ –∞—Å—Å–µ–º–±–ª–µ—Ä–∞](/v3/documentation/tvm/instructions).
>
> –¢–∞–∫–∂–µ –º—ã –æ–±—ä—è–≤–ª—è–µ–º —Ñ—É–Ω–∫—Ü–∏—é `to_tuple`. –û–Ω–∞ –ø—Ä–æ—Å—Ç–æ –∏–∑–º–µ–Ω—è–µ—Ç —Ç–∏–ø –¥–∞–Ω–Ω—ã—Ö –ª—é–±–æ–≥–æ –≤–≤–æ–¥–∞ –Ω–∞ –∫–æ—Ä—Ç–µ–∂, –ø–æ—ç—Ç–æ–º—É –±—É–¥—å—Ç–µ –æ—Å—Ç–æ—Ä–æ–∂–Ω—ã –ø—Ä–∏ –µ–µ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–∏.

### –ö–∞–∫ –ø–∏—Å–∞—Ç—å —Å–æ–±—Å—Ç–≤–µ–Ω–Ω—ã–µ —Ñ—É–Ω–∫—Ü–∏–∏ —Å –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–µ–º –∫–ª—é—á–µ–≤–æ–≥–æ —Å–ª–æ–≤–∞ `asm`

–ü—Ä–∏ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–∏ –ª—é–±—ã—Ö —Ñ—É–Ω–∫—Ü–∏–π –º—ã —Ñ–∞–∫—Ç–∏—á–µ—Å–∫–∏ –∏—Å–ø–æ–ª—å–∑—É–µ–º –∑–∞—Ä–∞–Ω–µ–µ –ø–æ–¥–≥–æ—Ç–æ–≤–ª–µ–Ω–Ω—ã–µ –¥–ª—è –Ω–∞—Å –º–µ—Ç–æ–¥—ã –≤–Ω—É—Ç—Ä–∏ `stdlib.fc`. –ù–æ –Ω–∞ —Å–∞–º–æ–º –¥–µ–ª–µ, –Ω–∞–º –¥–æ—Å—Ç—É–ø–Ω–æ –≥–æ—Ä–∞–∑–¥–æ –±–æ–ª—å—à–µ –≤–æ–∑–º–æ–∂–Ω–æ—Å—Ç–µ–π, –∏ –Ω–∞–º –Ω—É–∂–Ω–æ –Ω–∞—É—á–∏—Ç—å—Å—è –ø–∏—Å–∞—Ç—å –∏—Ö —Å–∞–º–∏–º.

–ù–∞–ø—Ä–∏–º–µ—Ä, —É –Ω–∞—Å –µ—Å—Ç—å –º–µ—Ç–æ–¥ `tpush`, –∫–æ—Ç–æ—Ä—ã–π –¥–æ–±–∞–≤–ª—è–µ—Ç —ç–ª–µ–º–µ–Ω—Ç –≤ `tuple`, –Ω–æ –±–µ–∑ `tpop`. –í —ç—Ç–æ–º —Å–ª—É—á–∞–µ –º—ã –¥–æ–ª–∂–Ω—ã —Å–¥–µ–ª–∞—Ç—å —ç—Ç–æ:

```func
;; ~ means it is modifying method
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP"; 
```

–ï—Å–ª–∏ –º—ã —Ö–æ—Ç–∏–º —É–∑–Ω–∞—Ç—å –¥–ª–∏–Ω—É `tuple` –¥–ª—è –∏—Ç–µ—Ä–∞—Ü–∏–∏, –º—ã –¥–æ–ª–∂–Ω—ã –Ω–∞–ø–∏—Å–∞—Ç—å –Ω–æ–≤—É—é —Ñ—É–Ω–∫—Ü–∏—é —Å –∏–Ω—Å—Ç—Ä—É–∫—Ü–∏–µ–π `TLEN` asm:

```func
int tuple_length (tuple t) asm "TLEN";
```

–ù–µ–∫–æ—Ç–æ—Ä—ã–µ –ø—Ä–∏–º–µ—Ä—ã —Ñ—É–Ω–∫—Ü–∏–π, —É–∂–µ –∏–∑–≤–µ—Å—Ç–Ω—ã—Ö –Ω–∞–º –∏–∑ stdlib.fc:

```func
slice begin_parse(cell c) asm "CTOS";
builder begin_cell() asm "NEWC";
cell end_cell(builder b) asm "ENDC";
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏:
>
> ["–ú–µ—Ç–æ–¥ –∏–∑–º–µ–Ω–µ–Ω–∏—è" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/statements#modifying-methods)
>
> ["stdlib" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib)
>
> ["–ò–Ω—Å—Ç—Ä—É–∫—Ü–∏–∏ TVM" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/tvm/instructions)

### –ò—Ç–µ—Ä–∞—Ü–∏—è n-–≤–ª–æ–∂–µ–Ω–Ω—ã—Ö –∫–æ—Ä—Ç–µ–∂–µ–π

–ò–Ω–æ–≥–¥–∞ –º—ã —Ö–æ—Ç–∏–º –∏—Ç–µ—Ä–∏—Ä–æ–≤–∞—Ç—å –≤–ª–æ–∂–µ–Ω–Ω—ã–µ –∫–æ—Ä—Ç–µ–∂–∏. –°–ª–µ–¥—É—é—â–∏–π –ø—Ä–∏–º–µ—Ä –≤—ã–ø–æ–ª–Ω–∏—Ç –∏—Ç–µ—Ä–∞—Ü–∏—é –∏ –≤—ã–≤–µ–¥–µ—Ç –≤—Å–µ —ç–ª–µ–º–µ–Ω—Ç—ã –≤ –∫–æ—Ä—Ç–µ–∂–µ –≤ —Ñ–æ—Ä–º–∞—Ç–µ `[[2,6],[1,[3,[3,5]]], 3]`, –Ω–∞—á–∏–Ω–∞—è —Å –∑–∞–≥–æ–ª–æ–≤–∫–∞

```func
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
forall X -> int is_tuple (X x) asm "ISTUPLE";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> (tuple) to_tuple (X x) asm "NOP";

;; define global variable
global int max_value;

() iterate_tuple (tuple t) impure {
    repeat (t.tuple_length()) {
        var value = t~tpop();
        if (is_tuple(value)) {
            tuple tuple_value = cast_to_tuple(value);
            iterate_tuple(tuple_value);
        }
        else {
            if(value > max_value) {
                max_value = value;
            }
        }
    }
}

() main () {
    tuple t = to_tuple([[2,6], [1, [3, [3, 5]]], 3]);
    int len = t.tuple_length();
    max_value = 0; ;; reset max_value;
    iterate_tuple(t); ;; iterate tuple and find max value
    ~dump(max_value); ;; 6
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ì–ª–æ–±–∞–ª—å–Ω—ã–µ –ø–µ—Ä–µ–º–µ–Ω–Ω—ã–µ" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/global_variables)
>
> ["~dump" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/builtins#dump-variable)
>
> ["–ò–Ω—Å—Ç—Ä—É–∫—Ü–∏–∏ TVM" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/tvm/instructions)

### –û—Å–Ω–æ–≤–Ω—ã–µ –æ–ø–µ—Ä–∞—Ü–∏–∏ —Å –∫–æ—Ä—Ç–µ–∂–∞–º–∏

```func
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

() main () {
    ;; creating an empty tuple
    tuple names = empty_tuple(); 
    
    ;; push new items
    names~tpush("Naito Narihira");
    names~tpush("Shiraki Shinichi");
    names~tpush("Akamatsu Hachemon");
    names~tpush("Takaki Yuichi");
    
    ;; pop last item
    slice last_name = names~tpop();

    ;; get first item
    slice first_name = names.first();

    ;; get an item by index
    slice best_name = names.at(2);

    ;; getting the length of the list 
    int number_names = names.tlen();
}
```

### –û–ø—Ä–µ–¥–µ–ª–µ–Ω–∏–µ —Ç–∏–ø–∞ X

–í —Å–ª–µ–¥—É—é—â–µ–º –ø—Ä–∏–º–µ—Ä–µ –ø—Ä–æ–≤–µ—Ä—è–µ—Ç—Å—è, —Å–æ–¥–µ—Ä–∂–∏—Ç—Å—è –ª–∏ –≤ –∫–æ—Ä—Ç–µ–∂–µ –∫–∞–∫–æ–µ-–ª–∏–±–æ –∑–Ω–∞—á–µ–Ω–∏–µ, –Ω–æ –∫–æ—Ä—Ç–µ–∂ —Å–æ–¥–µ—Ä–∂–∏—Ç –∑–Ω–∞—á–µ–Ω–∏—è X (cell, slice, int, tuple, int). –ù–∞–º –Ω—É–∂–Ω–æ –ø—Ä–æ–≤–µ—Ä–∏—Ç—å –∑–Ω–∞—á–µ–Ω–∏–µ –∏ –ø—Ä–∏–≤–µ—Å—Ç–∏ –µ–≥–æ –≤ —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤–∏–µ.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> int is_int (X x) asm "<{ TRY:<{ 0 PUSHINT ADD DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_cell (X x) asm "<{ TRY:<{ CTOS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_slice (X x) asm "<{ TRY:<{ SBITS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_tuple (X x) asm "ISTUPLE";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> cell cast_to_cell (X x) asm "NOP";
forall X -> slice cast_to_slice (X x) asm "NOP";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

forall X -> () resolve_type (X value) impure {
    ;; value here is of type X, since we dont know what is the exact value - we would need to check what is the value and then cast it
    
    if (is_null(value)) {
        ;; do something with the null
    }
    elseif (is_int(value)) {
        int valueAsInt = cast_to_int(value);
        ;; do something with the int
    }
    elseif (is_slice(value)) {
        slice valueAsSlice = cast_to_slice(value);
        ;; do something with the slice
    }
    elseif (is_cell(value)) {
        cell valueAsCell = cast_to_cell(value);
        ;; do something with the cell
    }
    elseif (is_tuple(value)) {
        tuple valueAsTuple = cast_to_tuple(value);
        ;; do something with the tuple
    }
}

() main () {
    ;; creating an empty tuple
    tuple stack = empty_tuple();
    ;; let's say we have tuple and do not know the exact types of them
    stack~tpush("Some text");
    stack~tpush(4);
    ;; we use var because we do not know type of value
    var value = stack~tpop();
    resolve_type(value);
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ò–Ω—Å—Ç—Ä—É–∫—Ü–∏–∏ TVM" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/tvm/instructions)

### –ö–∞–∫ –ø–æ–ª—É—á–∏—Ç—å —Ç–µ–∫—É—â–µ–µ –≤—Ä–µ–º—è

```func
int current_time = now();
  
if (current_time > 1672080143) {
    ;; do some stuff 
}
```

### –ö–∞–∫ —Å–≥–µ–Ω–µ—Ä–∏—Ä–æ–≤–∞—Ç—å —Å–ª—É—á–∞–π–Ω–æ–µ —á–∏—Å–ª–æ

:::caution —á–µ—Ä–Ω–æ–≤–∏–∫

–û–∑–Ω–∞–∫–æ–º—å—Ç–µ—Å—å —Å [–≥–µ–Ω–µ—Ä–∞—Ü–∏–µ–π —Å–ª—É—á–∞–π–Ω—ã—Ö —á–∏—Å–µ–ª](/v3/guidelines/smart-contracts/security/random-number-generation) –¥–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –¥–æ–ø–æ–ª–Ω–∏—Ç–µ–ª—å–Ω–æ–π –∏–Ω—Ñ–æ—Ä–º–∞—Ü–∏–∏.
:::

```func
randomize_lt(); ;; do this once

int a = rand(10);
int b = rand(1000000);
int c = random();
```

### –û–ø–µ—Ä–∞—Ü–∏–∏ –ø–æ –º–æ–¥—É–ª—é

–í –∫–∞—á–µ—Å—Ç–≤–µ –ø—Ä–∏–º–µ—Ä–∞ –ø—Ä–µ–¥–ø–æ–ª–æ–∂–∏–º, —á—Ç–æ –º—ã —Ö–æ—Ç–∏–º –≤—ã–ø–æ–ª–Ω–∏—Ç—å —Å–ª–µ–¥—É—é—â–µ–µ –≤—ã—á–∏—Å–ª–µ–Ω–∏–µ –≤—Å–µ—Ö 256 —á–∏—Å–µ–ª: `(xp + zp)*(xp-zp)`. –ü–æ—Å–∫–æ–ª—å–∫—É –±–æ–ª—å—à–∏–Ω—Å—Ç–≤–æ —ç—Ç–∏—Ö –æ–ø–µ—Ä–∞—Ü–∏–π –∏—Å–ø–æ–ª—å–∑—É—é—Ç—Å—è –≤ –∫—Ä–∏–ø—Ç–æ–≥—Ä–∞—Ñ–∏–∏, –≤ —Å–ª–µ–¥—É—é—â–µ–º –ø—Ä–∏–º–µ—Ä–µ –º—ã –∏—Å–ø–æ–ª—å–∑—É–µ–º –æ–ø–µ—Ä–∞—Ç–æ—Ä –ø–æ –º–æ–¥—É–ª—é –¥–ª—è –∫—Ä–∏–≤—ã—Ö –ú–æ–Ω—Ç–≥–æ–º–µ—Ä–∏.
–û–±—Ä–∞—Ç–∏—Ç–µ –≤–Ω–∏–º–∞–Ω–∏–µ, —á—Ç–æ xp+zp ‚Äî —ç—Ç–æ –¥–æ–ø—É—Å—Ç–∏–º–æ–µ –∏–º—è –ø–µ—Ä–µ–º–µ–Ω–Ω–æ–π (–±–µ–∑ –ø—Ä–æ–±–µ–ª–æ–≤ –º–µ–∂–¥—É –Ω–∏–º–∏).

```func
(int) modulo_operations (int xp, int zp) {  
   ;; 2^255 - 19 is a prime number for montgomery curves, meaning all operations should be done against its prime
   int prime = 57896044618658097711785492504343953926634992332820282019728792003956564819949; 

   ;; muldivmod handles the next two lines itself
   ;; int xp+zp = (xp + zp) % prime;
   ;; int xp-zp = (xp - zp + prime) % prime;
   (_, int xp+zp*xp-zp) = muldivmod(xp + zp, xp - zp, prime);
   return xp+zp*xp-zp;
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["muldivmod" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/tvm/instructions#A98C)

### –ö–∞–∫ –≤—ã–∑—ã–≤–∞—Ç—å –æ—à–∏–±–∫–∏

```func
int number = 198;

throw_if(35, number > 50); ;; the error will be triggered only if the number is greater than 50

throw_unless(39, number == 198); ;; the error will be triggered only if the number is NOT EQUAL to 198

throw(36); ;; the error will be triggered anyway
```

[–°—Ç–∞–Ω–¥–∞—Ä—Ç–Ω—ã–µ –∫–æ–¥—ã –∏—Å–∫–ª—é—á–µ–Ω–∏–π tvm](/v3/documentation/tvm/tvm-exit-codes)

### –ü–µ—Ä–µ–≤–æ—Ä–∞—á–∏–≤–∞–Ω–∏–µ –∫–æ—Ä—Ç–µ–∂–µ–π

–ü–æ—Å–∫–æ–ª—å–∫—É –∫–æ—Ä—Ç–µ–∂ —Ö—Ä–∞–Ω–∏—Ç –¥–∞–Ω–Ω—ã–µ –≤ –≤–∏–¥–µ —Å—Ç–µ–∫–∞, –∏–Ω–æ–≥–¥–∞ –Ω–∞–º –ø—Ä–∏—Ö–æ–¥–∏—Ç—Å—è –ø–µ—Ä–µ–≤–æ—Ä–∞—á–∏–≤–∞—Ç—å –µ–≥–æ, —á—Ç–æ–±—ã –ø—Ä–æ—á–∏—Ç–∞—Ç—å –¥–∞–Ω–Ω—ã–µ —Å –¥—Ä—É–≥–æ–≥–æ –∫–æ–Ω—Ü–∞.

```func
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

(tuple) reverse_tuple (tuple t1) {
    tuple t2 = empty_tuple();
    repeat (t1.tuple_length()) {
        var value = t1~tpop();
        t2~tpush(value);
    }
    return t2;
}

() main () {
    tuple t = to_tuple([1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
    tuple reversed_t = reverse_tuple(t);
    ~dump(reversed_t); ;; [10 9 8 7 6 5 4 3 2 1]
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["tpush()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#tpush)

### –ö–∞–∫ —É–¥–∞–ª–∏—Ç—å —ç–ª–µ–º–µ–Ω—Ç —Å –æ–ø—Ä–µ–¥–µ–ª–µ–Ω–Ω—ã–º –∏–Ω–¥–µ–∫—Å–æ–º –∏–∑ —Å–ø–∏—Å–∫–∞

```func
int tlen (tuple t) asm "TLEN";

(tuple, ()) remove_item (tuple old_tuple, int place) {
    tuple new_tuple = empty_tuple();

    int i = 0;
    while (i < old_tuple.tlen()) {
        int el = old_tuple.at(i);
        if (i != place) {
            new_tuple~tpush(el);
        }
        i += 1;  
    }
    return (new_tuple, ());
}

() main () {
    tuple numbers = empty_tuple();

    numbers~tpush(19);
    numbers~tpush(999);
    numbers~tpush(54);

    ~dump(numbers); ;; [19 999 54]

    numbers~remove_item(1); 

    ~dump(numbers); ;; [19 54]
}
```

### –û–ø—Ä–µ–¥–µ–ª–∏—Ç—å, —Ä–∞–≤–Ω—ã –ª–∏ —Å—Ä–µ–∑—ã

–ï—Å—Ç—å –¥–≤–∞ —Ä–∞–∑–Ω—ã—Ö —Å–ø–æ—Å–æ–±–∞ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å —Ä–∞–≤–µ–Ω—Å—Ç–≤–æ. –û–¥–∏–Ω –æ—Å–Ω–æ–≤–∞–Ω –Ω–∞ —Ö—ç—à–µ —Å—Ä–µ–∑–∞, –∞ –¥—Ä—É–≥–æ–π ‚Äî –Ω–∞ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–∏ –∏–Ω—Å—Ç—Ä—É–∫—Ü–∏–∏ SDEQ asm.

```func
int are_slices_equal_1? (slice a, slice b) {
    return a.slice_hash() == b.slice_hash();
}

int are_slices_equal_2? (slice a, slice b) asm "SDEQ";

() main () {
    slice a = "Some text";
    slice b = "Some text";
    ~dump(are_slices_equal_1?(a, b)); ;; -1 = true

    a = "Text";
    ;; We use literal `a` to get valid address inside slice from string containing address
    b = "EQDKbjIcfM6ezt8KjKJJLshZJJSqX7XOA4ff-W72r5gqPrHF"a;
    ~dump(are_slices_equal_2?(a, b)); ;; 0 = false
}
```

#### üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏

- ["slice_hash()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_hash)
- ["SDEQ" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/tvm/instructions#C705)

### –û–ø—Ä–µ–¥–µ–ª—è–µ–º, —Ä–∞–≤–Ω—ã –ª–∏ —è—á–µ–π–∫–∏

–ú—ã –º–æ–∂–µ–º –ª–µ–≥–∫–æ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å —Ä–∞–≤–µ–Ω—Å—Ç–≤–æ —è—á–µ–µ–∫ –Ω–∞ –æ—Å–Ω–æ–≤–µ –∏—Ö —Ö–µ—à–∞.

```func
int are_cells_equal? (cell a, cell b) {
    return a.cell_hash() == b.cell_hash();
}

() main () {
    cell a = begin_cell()
            .store_uint(123, 16)
            .end_cell();

    cell b = begin_cell()
            .store_uint(123, 16)
            .end_cell();

    ~dump(are_cells_equal?(a, b)); ;; -1 = true
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["cell_hash()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#cell_hash)

### –û–ø—Ä–µ–¥–µ–ª–∏—Ç—å, —Ä–∞–≤–Ω—ã –ª–∏ –∫–æ—Ä—Ç–µ–∂–∏

–ë–æ–ª–µ–µ –ø—Ä–æ–¥–≤–∏–Ω—É—Ç—ã–º –ø—Ä–∏–º–µ—Ä–æ–º –º–æ–∂–µ—Ç –±—ã—Ç—å –ø–æ–≤—Ç–æ—Ä–µ–Ω–∏–µ –∏ —Å—Ä–∞–≤–Ω–µ–Ω–∏–µ –∫–∞–∂–¥–æ–≥–æ –∏–∑ –∑–Ω–∞—á–µ–Ω–∏–π –∫–æ—Ä—Ç–µ–∂–∞. –ü–æ—Å–∫–æ–ª—å–∫—É –æ–Ω–∏ —Ä–∞–≤–Ω—ã X, –Ω–∞–º –Ω—É–∂–Ω–æ –ø—Ä–æ–≤–µ—Ä–∏—Ç—å –∏ –ø—Ä–∏–≤–µ—Å—Ç–∏ –∫ —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É—é—â–µ–º—É —Ç–∏–ø—É, –∏, –µ—Å–ª–∏ —ç—Ç–æ –∫–æ—Ä—Ç–µ–∂, –≤—ã–ø–æ–ª–Ω–∏—Ç—å –µ–≥–æ —Ä–µ–∫—É—Ä—Å–∏–≤–Ω—É—é –∏—Ç–µ—Ä–∞—Ü–∏—é.

```func
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> cell cast_to_cell (X x) asm "NOP";
forall X -> slice cast_to_slice (X x) asm "NOP";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> int is_null (X x) asm "ISNULL";
forall X -> int is_int (X x) asm "<{ TRY:<{ 0 PUSHINT ADD DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_cell (X x) asm "<{ TRY:<{ CTOS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_slice (X x) asm "<{ TRY:<{ SBITS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_tuple (X x) asm "ISTUPLE";
int are_slices_equal? (slice a, slice b) asm "SDEQ";

int are_cells_equal? (cell a, cell b) {
    return a.cell_hash() == b.cell_hash();
}

(int) are_tuples_equal? (tuple t1, tuple t2) {
    int equal? = -1; ;; initial value to true
    
    if (t1.tuple_length() != t2.tuple_length()) {
        ;; if tuples are differ in length they cannot be equal
        return 0;
    }

    int i = t1.tuple_length();
    
    while (i > 0 & equal?) {
        var v1 = t1~tpop();
        var v2 = t2~tpop();
        
        if (is_null(t1) & is_null(t2)) {
            ;; nulls are always equal
        }
        elseif (is_int(v1) & is_int(v2)) {
            if (cast_to_int(v1) != cast_to_int(v2)) {
                equal? = 0;
            }
        }
        elseif (is_slice(v1) & is_slice(v2)) {
            if (~ are_slices_equal?(cast_to_slice(v1), cast_to_slice(v2))) {
                equal? = 0;
            }
        }
        elseif (is_cell(v1) & is_cell(v2)) {
            if (~ are_cells_equal?(cast_to_cell(v1), cast_to_cell(v2))) {
                equal? = 0;
            }
        }
        elseif (is_tuple(v1) & is_tuple(v2)) {
            ;; recursively determine nested tuples
            if (~ are_tuples_equal?(cast_to_tuple(v1), cast_to_tuple(v2))) {
                equal? = 0;
            }
        }
        else {
            equal? = 0;
        }

        i -= 1;
    }

    return equal?;
}

() main () {
    tuple t1 = cast_to_tuple([[2, 6], [1, [3, [3, 5]]], 3]);
    tuple t2 = cast_to_tuple([[2, 6], [1, [3, [3, 5]]], 3]);

    ~dump(are_tuples_equal?(t1, t2)); ;; -1 
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["cell_hash()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#cell_hash)
>
> ["–ò–Ω—Å—Ç—Ä—É–∫—Ü–∏–∏ TVM" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/tvm/instructions)

### –ì–µ–Ω–µ—Ä–∞—Ü–∏—è –≤–Ω—É—Ç—Ä–µ–Ω–Ω–µ–≥–æ –∞–¥—Ä–µ—Å–∞

–ù–∞–º –Ω—É–∂–Ω–æ —Å–≥–µ–Ω–µ—Ä–∏—Ä–æ–≤–∞—Ç—å –≤–Ω—É—Ç—Ä–µ–Ω–Ω–∏–π –∞–¥—Ä–µ—Å, –∫–æ–≥–¥–∞ –Ω–∞—à –∫–æ–Ω—Ç—Ä–∞–∫—Ç –¥–æ–ª–∂–µ–Ω —Ä–∞–∑–≤–µ—Ä–Ω—É—Ç—å –Ω–æ–≤—ã–π –∫–æ–Ω—Ç—Ä–∞–∫—Ç, –Ω–æ –º—ã –Ω–µ –∑–Ω–∞–µ–º –µ–≥–æ –∞–¥—Ä–µ—Å. –ü—Ä–µ–¥–ø–æ–ª–æ–∂–∏–º, —É –Ω–∞—Å —É–∂–µ –µ—Å—Ç—å `state_init` ‚Äî –∫–æ–¥ –∏ –¥–∞–Ω–Ω—ã–µ –Ω–æ–≤–æ–≥–æ –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞.

–°–æ–∑–¥–∞–µ—Ç –≤–Ω—É—Ç—Ä–µ–Ω–Ω–∏–π –∞–¥—Ä–µ—Å –¥–ª—è —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É—é—â–µ–≥–æ MsgAddressInt TLB.

```func
(slice) generate_internal_address (int workchain_id, cell state_init) {
    ;; addr_std$10 anycast:(Maybe Anycast) workchain_id:int8 address:bits256  = MsgAddressInt;

    return begin_cell()
        .store_uint(2, 2) ;; addr_std$10
        .store_uint(0, 1) ;; anycast nothing
        .store_int(workchain_id, 8) ;; workchain_id: -1
        .store_uint(cell_hash(state_init), 256)
    .end_cell().begin_parse();
}

() main () {
    slice deploy_address = generate_internal_address(workchain(), state_init);
    ;; then we can deploy new contract
}
```

> üí° –ü—Ä–∏–º–µ—á–∞–Ω–∏–µ
>
> –í —ç—Ç–æ–º –ø—Ä–∏–º–µ—Ä–µ –º—ã –∏—Å–ø–æ–ª—å–∑—É–µ–º `workchain()` –¥–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –∏–¥–µ–Ω—Ç–∏—Ñ–∏–∫–∞—Ç–æ—Ä–∞ –≤–æ—Ä–∫—á–µ–π–Ω–∞. –ü–æ–¥—Ä–æ–±–Ω–µ–µ –æ –∏–¥–µ–Ω—Ç–∏—Ñ–∏–∫–∞—Ç–æ—Ä–µ –≤–æ—Ä–∫—á–µ–π–Ω–∞ –º–æ–∂–Ω–æ —É–∑–Ω–∞—Ç—å –≤ [–¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/addresses#workchain-id).

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["cell_hash()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#cell_hash)

### –ì–µ–Ω–µ—Ä–∞—Ü–∏—è –≤–Ω–µ—à–Ω–µ–≥–æ –∞–¥—Ä–µ—Å–∞

–ú—ã –∏—Å–ø–æ–ª—å–∑—É–µ–º —Å—Ö–µ–º—É TL-B –∏–∑ [block.tlb](https://github.com/ton-blockchain/ton/blob/24dc184a2ea67f9c47042b4104bbb4d82289fac1/crypto/block/block.tlb#L101C1-L101C12), —á—Ç–æ–±—ã –ø–æ–Ω—è—Ç—å, –∫–∞–∫ –Ω–∞–º –Ω—É–∂–Ω–æ —Å–æ–∑–¥–∞—Ç—å –∞–¥—Ä–µ—Å –≤ —ç—Ç–æ–º —Ñ–æ—Ä–º–∞—Ç–µ.

```func
(int) ubitsize (int a) asm "UBITSIZE";

slice generate_external_address (int address) {
    ;; addr_extern$01 len:(## 9) external_address:(bits len) = MsgAddressExt;
    
    int address_length = ubitsize(address);
    
    return begin_cell()
        .store_uint(1, 2) ;; addr_extern$01
        .store_uint(address_length, 9)
        .store_uint(address, address_length)
    .end_cell().begin_parse();
}
```

–ü–æ—Å–∫–æ–ª—å–∫—É –Ω–∞–º –Ω—É–∂–Ω–æ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –±–∏—Ç, –∑–∞–Ω–∏–º–∞–µ–º—ã—Ö –∞–¥—Ä–µ—Å–æ–º, –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ —Ç–∞–∫–∂–µ [–æ–±—ä—è–≤–∏—Ç—å —Ñ—É–Ω–∫—Ü–∏—é asm](#how-to-write-own-functions-using-asm-keyword) —Å –∫–æ–¥–æ–º –æ–ø–µ—Ä–∞—Ü–∏–∏ `UBITSIZE`, –∫–æ—Ç–æ—Ä–∞—è –≤–µ—Ä–Ω–µ—Ç –º–∏–Ω–∏–º–∞–ª—å–Ω–æ–µ –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –±–∏—Ç, –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ–µ –¥–ª—è —Ö—Ä–∞–Ω–µ–Ω–∏—è —á–∏—Å–ª–∞.

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ò–Ω—Å—Ç—Ä—É–∫—Ü–∏–∏ TVM" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/tvm/instructions#B603)

### –ö–∞–∫ —Ö—Ä–∞–Ω–∏—Ç—å –∏ –∑–∞–≥—Ä—É–∂–∞—Ç—å —Å–ª–æ–≤–∞—Ä—å –≤ –ª–æ–∫–∞–ª—å–Ω–æ–º —Ö—Ä–∞–Ω–∏–ª–∏—â–µ

–õ–æ–≥–∏–∫–∞ –∑–∞–≥—Ä—É–∑–∫–∏ —Å–ª–æ–≤–∞—Ä—è

```func
slice local_storage = get_data().begin_parse();
cell dictionary_cell = new_dict();
if (~ slice_empty?(local_storage)) {
    dictionary_cell = local_storage~load_dict();
}
```

–í —Ç–æ –≤—Ä–µ–º—è –∫–∞–∫ –ª–æ–≥–∏–∫–∞ —Ö—Ä–∞–Ω–µ–Ω–∏—è —Å–ª–æ–≤–∞—Ä—è –ø–æ—Ö–æ–∂–∞ –Ω–∞ —Å–ª–µ–¥—É—é—â–∏–π –ø—Ä–∏–º–µ—Ä:

```func
set_data(begin_cell().store_dict(dictionary_cell).end_cell());
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["get_data()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#get_data)
>
> ["new_dict()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#new_dict)
>
> ["slice_empty?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_empty)
>
> ["load_dict()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#load_dict)
>
> ["~" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/statements#unary-operators)

### –ö–∞–∫ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å –ø—Ä–æ—Å—Ç–æ–µ —Å–æ–æ–±—â–µ–Ω–∏–µ

–û–±—ã—á–Ω–æ –º—ã –æ—Ç–ø—Ä–∞–≤–ª—è–µ–º tons —Å –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–µ–º –≤ –≤–∏–¥–µ –ø—Ä–æ—Å—Ç–æ–≥–æ —Å–æ–æ–±—â–µ–Ω–∏—è. –ß—Ç–æ–±—ã —É–∫–∞–∑–∞—Ç—å, —á—Ç–æ —Ç–µ–ª–æ —Å–æ–æ–±—â–µ–Ω–∏—è —è–≤–ª—è–µ—Ç—Å—è `comment`, –º—ã –¥–æ–ª–∂–Ω—ã —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å `32 bits` –ø–µ—Ä–µ–¥ —Ç–µ–∫—Å—Ç–æ–º —Å–æ–æ–±—â–µ–Ω–∏—è –≤ 0.

```func
cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(100) ;; amount of nanoTons to send
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(0, 32) ;; zero opcode - means simple transfer message with comment
    .store_slice("Hello from FunC!") ;; comment
.end_cell();
send_raw_message(msg, 3); ;; mode 3 - pay fees separately, ignore errors
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ú–∞–∫–µ—Ç —Å–æ–æ–±—â–µ–Ω–∏—è" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/message-management/sending-messages)

### –ö–∞–∫ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å —Å–æ–æ–±—â–µ–Ω–∏–µ —Å –ø–æ–º–æ—â—å—é –≤—Ö–æ–¥—è—â–µ–≥–æ –∞–∫–∫–∞—É–Ω—Ç–∞

–ü—Ä–∏–º–µ—Ä –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞ –Ω–∏–∂–µ –ø—Ä–∏–≥–æ–¥–∏—Ç—Å—è –Ω–∞–º, –µ—Å–ª–∏ –Ω–∞–º –Ω—É–∂–Ω–æ –≤—ã–ø–æ–ª–Ω–∏—Ç—å –∫–∞–∫–∏–µ-–ª–∏–±–æ –¥–µ–π—Å—Ç–≤–∏—è –º–µ–∂–¥—É –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª–µ–º –∏ –æ—Å–Ω–æ–≤–Ω—ã–º –∫–æ–Ω—Ç—Ä–∞–∫—Ç–æ–º, —Ç–æ –µ—Å—Ç—å –Ω–∞–º –Ω—É–∂–µ–Ω –ø—Ä–æ–∫—Å–∏-–∫–æ–Ω—Ç—Ä–∞–∫—Ç.

```func
() recv_internal (slice in_msg_body) {
    {-
        This is a simple example of a proxy-contract.
        It will expect in_msg_body to contain message mode, body and destination address to be sent to.
    -}

    int mode = in_msg_body~load_uint(8); ;; first byte will contain msg mode
    slice addr = in_msg_body~load_msg_addr(); ;; then we parse the destination address
    slice body = in_msg_body; ;; everything that is left in in_msg_body will be our new message's body

    cell msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(addr)
        .store_coins(100) ;; just for example
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
        .store_slice(body)
    .end_cell();
    send_raw_message(msg, mode);
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ú–∞–∫–µ—Ç —Å–æ–æ–±—â–µ–Ω–∏—è" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/message-management/sending-messages)
>
> ["load_msg_addr()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#load_msg_addr)

### –ö–∞–∫ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å —Å–æ–æ–±—â–µ–Ω–∏–µ —Å–æ –≤—Å–µ–º –±–∞–ª–∞–Ω—Å–æ–º

–ï—Å–ª–∏ –Ω–∞–º –Ω—É–∂–Ω–æ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å –≤–µ—Å—å –±–∞–ª–∞–Ω—Å —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞, —Ç–æ –≤ —ç—Ç–æ–º —Å–ª—É—á–∞–µ –Ω–∞–º –Ω—É–∂–Ω–æ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å –æ—Ç–ø—Ä–∞–≤–∏—Ç—å `mode 128`. –ü—Ä–∏–º–µ—Ä–æ–º —Ç–∞–∫–æ–≥–æ —Å–ª—É—á–∞—è –º–æ–∂–µ—Ç —Å–ª—É–∂–∏—Ç—å –ø—Ä–æ–∫—Å–∏-–∫–æ–Ω—Ç—Ä–∞–∫—Ç, –∫–æ—Ç–æ—Ä—ã–π –ø—Ä–∏–Ω–∏–º–∞–µ—Ç –ø–ª–∞—Ç–µ–∂–∏ –∏ –ø–µ—Ä–µ—Å—ã–ª–∞–µ—Ç –∏—Ö –æ—Å–Ω–æ–≤–Ω–æ–º—É –∫–æ–Ω—Ç—Ä–∞–∫—Ç—É.

```func
cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(0) ;; we don't care about this value right now
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(0, 32) ;; zero opcode - means simple transfer message with comment
    .store_slice("Hello from FunC!") ;; comment
.end_cell();
send_raw_message(msg, 128); ;; mode = 128 is used for messages that are to carry all the remaining balance of the current smart contract
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ú–∞–∫–µ—Ç —Å–æ–æ–±—â–µ–Ω–∏—è" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/message-management/sending-messages)
>
> ["–†–µ–∂–∏–º—ã —Å–æ–æ–±—â–µ–Ω–∏—è" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#send_raw_message)

### –ö–∞–∫ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å —Å–æ–æ–±—â–µ–Ω–∏–µ —Å –¥–ª–∏–Ω–Ω—ã–º —Ç–µ–∫—Å—Ç–æ–≤—ã–º –∫–æ–º–º–µ–Ω—Ç–∞—Ä–∏–µ–º

–ö–∞–∫ –º—ã –∑–Ω–∞–µ–º, –≤ –æ–¥–Ω—É `cell` –ø–æ–º–µ—â–∞–µ—Ç—Å—è —Ç–æ–ª—å–∫–æ 127 —Å–∏–º–≤–æ–ª–æ–≤ (< 1023 –±–∏—Ç). –ï—Å–ª–∏ –Ω–∞–º –Ω—É–∂–Ω–æ –±–æ–ª—å—à–µ - –Ω—É–∂–Ω–æ –æ—Ä–≥–∞–Ω–∏–∑–æ–≤–∞—Ç—å —è—á–µ–π–∫–∏ –∑–º–µ–π–∫–æ–π.

```func
{-
    If we want to send a message with really long comment, we should split the comment to several slices.
    Each slice should have <1023 bits of data (127 chars).
    Each slice should have a reference to the next one, forming a snake-like structure.
-}

cell body = begin_cell()
    .store_uint(0, 32) ;; zero opcode - simple message with comment
    .store_slice("long long long message...")
    .store_ref(begin_cell()
        .store_slice(" you can store string of almost any length here.")
        .store_ref(begin_cell()
            .store_slice(" just don't forget about the 127 chars limit for each slice")
        .end_cell())
    .end_cell())
.end_cell();

cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    ;; We use literal `a` to get valid address inside slice from string containing address 
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(100) ;; amount of nanoTons to send
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1) ;; default message headers (see sending messages page)
    .store_uint(1, 1) ;; we want to store body as a ref
    .store_ref(body)
.end_cell();
send_raw_message(msg, 3); ;; mode 3 - pay fees separately, ignore errors
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–í–Ω—É—Ç—Ä–µ–Ω–Ω–∏–µ —Å–æ–æ–±—â–µ–Ω–∏—è" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/message-management/internal-messages)

### –ö–∞–∫ –ø–æ–ª—É—á–∏—Ç—å —Ç–æ–ª—å–∫–æ –±–∏—Ç—ã –¥–∞–Ω–Ω—ã—Ö –∏–∑ —Å—Ä–µ–∑–∞ (–±–µ–∑ —Å—Å—ã–ª–æ–∫)

–ï—Å–ª–∏ –Ω–∞—Å –Ω–µ –∏–Ω—Ç–µ—Ä–µ—Å—É—é—Ç `refs` –≤–Ω—É—Ç—Ä–∏ `slice`, —Ç–æ –º—ã –º–æ–∂–µ–º –ø–æ–ª—É—á–∏—Ç—å –æ—Ç–¥–µ–ª—å–Ω—ã–µ –¥–∞–Ω–Ω—ã–µ –∏ —Ä–∞–±–æ—Ç–∞—Ç—å —Å –Ω–∏–º–∏.

```func
slice s = begin_cell()
    .store_slice("Some data bits...")
    .store_ref(begin_cell().end_cell()) ;; some references
    .store_ref(begin_cell().end_cell()) ;; some references
.end_cell().begin_parse();

slice s_only_data = s.preload_bits(s.slice_bits());
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ü—Ä–∏–º–∏—Ç–∏–≤—ã —Å—Ä–µ–∑–æ–≤" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#slice-primitives)
>
> ["preload_bits()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#preload_bits)
>
> ["slice_bits()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_bits)

### –ö–∞–∫ –æ–ø—Ä–µ–¥–µ–ª–∏—Ç—å —Å–æ–±—Å—Ç–≤–µ–Ω–Ω—ã–π –º–µ—Ç–æ–¥ –∏–∑–º–µ–Ω–µ–Ω–∏—è

–ú–µ—Ç–æ–¥—ã –∏–∑–º–µ–Ω–µ–Ω–∏—è –ø–æ–∑–≤–æ–ª—è—é—Ç –∏–∑–º–µ–Ω—è—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø—Ä–µ–¥–µ–ª–∞—Ö –æ–¥–Ω–æ–π –ø–µ—Ä–µ–º–µ–Ω–Ω–æ–π. –≠—Ç–æ –º–æ–∂–Ω–æ —Å—Ä–∞–≤–Ω–∏—Ç—å —Å–æ —Å—Å—ã–ª–∫–∞–º–∏ –≤ –¥—Ä—É–≥–∏—Ö —è–∑—ã–∫–∞—Ö –ø—Ä–æ–≥—Ä–∞–º–º–∏—Ä–æ–≤–∞–Ω–∏—è.

```func
(slice, (int)) load_digit (slice s) {
    int x = s~load_uint(8); ;; load 8 bits (one char) from slice
    x -= 48; ;; char '0' has code of 48, so we substract it to get the digit as a number
    return (s, (x)); ;; return our modified slice and loaded digit
}

() main () {
    slice s = "258";
    int c1 = s~load_digit();
    int c2 = s~load_digit();
    int c3 = s~load_digit();
    ;; here s is equal to "", and c1 = 2, c2 = 5, c3 = 8
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ú–µ—Ç–æ–¥—ã –∏–∑–º–µ–Ω–µ–Ω–∏—è" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/statements#modifying-methods)

### –ö–∞–∫ –≤–æ–∑–≤–µ—Å—Ç–∏ —á–∏—Å–ª–æ –≤ —Å—Ç–µ–ø–µ–Ω—å n

```func
;; Unoptimized variant
int pow (int a, int n) {
    int i = 0;
    int value = a;
    while (i < n - 1) {
        a *= value;
        i += 1;
    }
    return a;
}

;; Optimized variant
(int) binpow (int n, int e) {
    if (e == 0) {
        return 1;
    }
    if (e == 1) {
        return n;
    }
    int p = binpow(n, e / 2);
    p *= p;
    if ((e % 2) == 1) {
        p *= n;
    }
    return p;
}

() main () {
    int num = binpow(2, 3);
    ~dump(num); ;; 8
}
```

### –ö–∞–∫ –ø—Ä–µ–æ–±—Ä–∞–∑–æ–≤–∞—Ç—å —Å—Ç—Ä–æ–∫—É –≤ —Ü–µ–ª–æ–µ —á–∏—Å–ª–æ

```func
slice string_number = "26052021";
int number = 0;

while (~ string_number.slice_empty?()) {
    int char = string_number~load_uint(8);
    number = (number * 10) + (char - 48); ;; we use ASCII table
}

~dump(number);
```

### –ö–∞–∫ –ø—Ä–µ–æ–±—Ä–∞–∑–æ–≤–∞—Ç—å —Ü–µ–ª–æ–µ —á–∏—Å–ª–æ –≤ —Å—Ç—Ä–æ–∫—É

```func
int n = 261119911;
builder string = begin_cell();
tuple chars = null();
do {
    int r = n~divmod(10);
    chars = cons(r + 48, chars);
} until (n == 0);
do {
    int char = chars~list_next();
    string~store_uint(char, 8);
} until (null?(chars));

slice result = string.end_cell().begin_parse();
~dump(result);
```

### –ö–∞–∫ –ø–µ—Ä–µ–±–∏—Ä–∞—Ç—å —Å–ª–æ–≤–∞—Ä–∏

–°–ª–æ–≤–∞—Ä–∏ –æ—á–µ–Ω—å –ø–æ–ª–µ–∑–Ω—ã –ø—Ä–∏ —Ä–∞–±–æ—Ç–µ —Å –±–æ–ª—å—à–∏–º –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ–º –¥–∞–Ω–Ω—ã—Ö. –ú—ã –º–æ–∂–µ–º –ø–æ–ª—É—á–∏—Ç—å –º–∏–Ω–∏–º–∞–ª—å–Ω—ã–µ –∏ –º–∞–∫—Å–∏–º–∞–ª—å–Ω—ã–µ –∑–Ω–∞—á–µ–Ω–∏—è –∫–ª—é—á–µ–π, –∏—Å–ø–æ–ª—å–∑—É—è –≤—Å—Ç—Ä–æ–µ–Ω–Ω—ã–µ –º–µ—Ç–æ–¥—ã `dict_get_min?` –∏ `dict_get_max?` —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤–µ–Ω–Ω–æ. –ö—Ä–æ–º–µ —Ç–æ–≥–æ, –º—ã –º–æ–∂–µ–º –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å `dict_get_next?` –¥–ª—è –ø–µ—Ä–µ–±–∏—Ä–∞–Ω–∏—è —Å–ª–æ–≤–∞—Ä—è.

```func
cell d = new_dict();
d~udict_set(256, 1, "value 1");
d~udict_set(256, 5, "value 2");
d~udict_set(256, 12, "value 3");

;; iterate keys from small to big
(int key, slice val, int flag) = d.udict_get_min?(256);
while (flag) {
    ;; do something with pair key->val
    
    (key, val, flag) = d.udict_get_next?(256, key);
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–ü—Ä–∏–º–∏—Ç–∏–≤—ã —Å–ª–æ–≤–∞—Ä–µ–π" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#dictionaries-primitives)
>
> ["dict_get_max?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_get_max)
>
> ["dict_get_min?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_get_min)
>
> ["dict_get_next?()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_get_next)
>
> ["dict_set()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_set)

### –ö–∞–∫ —É–¥–∞–ª–∏—Ç—å –∑–Ω–∞—á–µ–Ω–∏–µ –∏–∑ —Å–ª–æ–≤–∞—Ä–µ–π

```func
cell names = new_dict();
names~udict_set(256, 27, "Alice");
names~udict_set(256, 25, "Bob");

names~udict_delete?(256, 27);

(slice val, int key) = names.udict_get?(256, 27);
~dump(val); ;; null() -> means that key was not found in a dictionary
```

### –ö–∞–∫ —Ä–µ–∫—É—Ä—Å–∏–≤–Ω–æ –ø–µ—Ä–µ–±—Ä–∞—Ç—å –¥–µ—Ä–µ–≤–æ —è—á–µ–µ–∫

–ö–∞–∫ –º—ã –∑–Ω–∞–µ–º, –æ–¥–Ω–∞ `cell` –º–æ–∂–µ—Ç —Ö—Ä–∞–Ω–∏—Ç—å –¥–æ `1023 bits` –¥–∞–Ω–Ω—ã—Ö –∏ –¥–æ `4 refs`. –ß—Ç–æ–±—ã –æ–±–æ–π—Ç–∏ —ç—Ç–æ –æ–≥—Ä–∞–Ω–∏—á–µ–Ω–∏–µ, –º—ã –º–æ–∂–µ–º –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å –¥–µ—Ä–µ–≤–æ —è—á–µ–µ–∫, –Ω–æ –¥–ª—è —ç—Ç–æ–≥–æ –Ω–∞–º –Ω—É–∂–Ω–æ –∏–º–µ—Ç—å –≤–æ–∑–º–æ–∂–Ω–æ—Å—Ç—å –ø–µ—Ä–µ–±–∏—Ä–∞—Ç—å –µ–≥–æ –¥–ª—è –ø—Ä–∞–≤–∏–ª—å–Ω–æ–π –æ–±—Ä–∞–±–æ—Ç–∫–∏ –¥–∞–Ω–Ω—ã—Ö.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() main () {
    ;; just some cell for example
    cell c = begin_cell()
        .store_uint(1, 16)
        .store_ref(begin_cell()
            .store_uint(2, 16)
        .end_cell())
        .store_ref(begin_cell()
            .store_uint(3, 16)
            .store_ref(begin_cell()
                .store_uint(4, 16)
            .end_cell())
            .store_ref(begin_cell()
                .store_uint(5, 16)
            .end_cell())
        .end_cell())
    .end_cell();

    ;; creating tuple with no data, which plays the role of stack
    tuple stack = null();
    ;; bring the main cell into the stack to process it in the loop
    stack~push_back(c);
    ;; do it until stack is not null
    while (~ stack.is_null()) {
        ;; get the cell from the stack and convert it to a slice to be able to process it
        slice s = stack~pop_back().begin_parse();

        ;; do something with s data

        ;; if the current slice has any refs, add them to stack
        repeat (s.slice_refs()) {
            stack~push_back(s~load_ref());
        }
    }
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–°–ø–∏—Å–∫–∏ –≤ —Å—Ç–∏–ª–µ Lisp" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#lisp-style-lists)
>
> ["null()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#null)
>
> ["slice_refs()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_refs)

### –ö–∞–∫ –ø–µ—Ä–µ–±–∏—Ä–∞—Ç—å —Å–ø–∏—Å–∫–∏ –≤ —Å—Ç–∏–ª–µ lisp

–¢–∏–ø –¥–∞–Ω–Ω—ã—Ö –∫–æ—Ä—Ç–µ–∂–∞ –º–æ–∂–µ—Ç —Å–æ–¥–µ—Ä–∂–∞—Ç—å –¥–æ 255 –∑–Ω–∞—á–µ–Ω–∏–π. –ï—Å–ª–∏ —ç—Ç–æ–≥–æ –Ω–µ–¥–æ—Å—Ç–∞—Ç–æ—á–Ω–æ, —Ç–æ —Å–ª–µ–¥—É–µ—Ç –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å —Å–ø–∏—Å–æ–∫ –≤ —Å—Ç–∏–ª–µ lisp. –ú—ã –º–æ–∂–µ–º –ø–æ–º–µ—Å—Ç–∏—Ç—å –∫–æ—Ä—Ç–µ–∂ –≤–Ω—É—Ç—Ä—å –∫–æ—Ä—Ç–µ–∂–∞, –æ–±–æ–π–¥—è —Ç–∞–∫–∏–º –æ–±—Ä–∞–∑–æ–º –æ–≥—Ä–∞–Ω–∏—á–µ–Ω–∏–µ.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() main () {
    ;; some example list
    tuple l = null();
    l~push_back(1);
    l~push_back(2);
    l~push_back(3);

    ;; iterating through elements
    ;; note that this iteration is in reversed order
    while (~ l.is_null()) {
        var x = l~pop_back();

        ;; do something with x
    }
}
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> ["–°–ø–∏—Å–∫–∏ –≤ —Å—Ç–∏–ª–µ Lisp" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#lisp-style-lists)
>
> ["null()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib/#null)

### –ö–∞–∫ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å —Å–æ–æ–±—â–µ–Ω–∏–µ –æ —Ä–∞–∑–≤–µ—Ä—Ç—ã–≤–∞–Ω–∏–∏ (—Ç–æ–ª—å–∫–æ —Å stateInit, —Å stateInit –∏ body)

```func
() deploy_with_stateinit(cell message_header, cell state_init) impure {
  var msg = begin_cell()
    .store_slice(begin_parse(msg_header))
    .store_uint(2 + 1, 2) ;; init:(Maybe (Either StateInit ^StateInit))
    .store_uint(0, 1) ;; body:(Either X ^X)
    .store_ref(state_init)
    .end_cell();

  ;; mode 64 - carry the remaining value in the new message
  send_raw_message(msg, 64); 
}

() deploy_with_stateinit_body(cell message_header, cell state_init, cell body) impure {
  var msg = begin_cell()
    .store_slice(begin_parse(msg_header))
    .store_uint(2 + 1, 2) ;; init:(Maybe (Either StateInit ^StateInit))
    .store_uint(1, 1) ;; body:(Either X ^X)
    .store_ref(state_init)
    .store_ref(body)
    .end_cell();

  ;; mode 64 - carry the remaining value in the new message
  send_raw_message(msg, 64); 
}
```

### –ö–∞–∫ —Å–æ–∑–¥–∞—Ç—å —è—á–µ–π–∫—É stateInit

```func
() build_stateinit(cell init_code, cell init_data) {
  var state_init = begin_cell()
    .store_uint(0, 1) ;; split_depth:(Maybe (## 5))
    .store_uint(0, 1) ;; special:(Maybe TickTock)
    .store_uint(1, 1) ;; (Maybe ^Cell)
    .store_uint(1, 1) ;; (Maybe ^Cell)
    .store_uint(0, 1) ;; (HashmapE 256 SimpleLib)
    .store_ref(init_code)
    .store_ref(init_data)
    .end_cell();
}
```

### –ö–∞–∫ —Ä–∞—Å—Å—á–∏—Ç–∞—Ç—å –∞–¥—Ä–µ—Å –∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞ (–∏—Å–ø–æ–ª—å–∑—É—è stateInit)

```func
() calc_address(cell state_init) {
  var future_address = begin_cell() 
    .store_uint(2, 2) ;; addr_std$10
    .store_uint(0, 1) ;; anycast:(Maybe Anycast)
    .store_uint(0, 8) ;; workchain_id:int8
    .store_uint(cell_hash(state_init), 256) ;; address:bits256
    .end_cell();
}
```

### –ö–∞–∫ –æ–±–Ω–æ–≤–∏—Ç—å –ª–æ–≥–∏–∫—É —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞

–ù–∏–∂–µ –ø—Ä–µ–¥—Å—Ç–∞–≤–ª–µ–Ω –ø—Ä–æ—Å—Ç–æ–π —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç `–°ounterV1`, –∫–æ—Ç–æ—Ä—ã–π –∏–º–µ–µ—Ç —Ñ—É–Ω–∫—Ü–∏–æ–Ω–∞–ª—å–Ω–æ—Å—Ç—å –¥–ª—è —É–≤–µ–ª–∏—á–µ–Ω–∏—è —Å—á–µ—Ç—á–∏–∫–∞ –∏ –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è –ª–æ–≥–∏–∫–∏ —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞.

```func
() recv_internal (slice in_msg_body) {
    int op = in_msg_body~load_uint(32);
    
    if (op == op::increase) {
        int increase_by = in_msg_body~load_uint(32);
        ctx_counter += increase_by;
        save_data();
        return ();
    }

    if (op == op::upgrade) {
        cell code = in_msg_body~load_ref();
        set_code(code);
        return ();
    }
}
```

–ü–æ—Å–ª–µ —Ä–∞–±–æ—Ç—ã —Å–æ —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç–æ–º –≤—ã –ø–æ–Ω–∏–º–∞–µ—Ç–µ, —á—Ç–æ –≤–∞–º –Ω–µ —Ö–≤–∞—Ç–∞–µ—Ç —Ñ—É–Ω–∫—Ü–∏–∏ —Å–æ–∫—Ä–∞—â–µ–Ω–∏—è —Å—á–µ—Ç—á–∏–∫–∞. –í–∞–º –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ —Å–∫–æ–ø–∏—Ä–æ–≤–∞—Ç—å –∫–æ–¥ —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç–∞ `CounterV1` –∏ —Ä—è–¥–æ–º —Å —Ñ—É–Ω–∫—Ü–∏–µ–π `increase` –¥–æ–±–∞–≤–∏—Ç—å –Ω–æ–≤—É—é —Ñ—É–Ω–∫—Ü–∏—é `decrease`. –¢–µ–ø–µ—Ä—å –≤–∞—à –∫–æ–¥ –≤—ã–≥–ª—è–¥–∏—Ç —Ç–∞–∫:

```func
() recv_internal (slice in_msg_body) {
    int op = in_msg_body~load_uint(32);
    
    if (op == op::increase) {
        int increase_by = in_msg_body~load_uint(32);
        ctx_counter += increase_by;
        save_data();
        return ();
    }

    if (op == op::decrease) {
        int decrease_by = in_msg_body~load_uint(32);
        ctx_counter -= increase_by;
        save_data();
        return ();
    }

    if (op == op::upgrade) {
        cell code = in_msg_body~load_ref();
        set_code(code);
        return ();
    }
}
```

–ö–∞–∫ —Ç–æ–ª—å–∫–æ —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç `CounterV2` –±—É–¥–µ—Ç –≥–æ—Ç–æ–≤, –≤–∞–º –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ —Å–∫–æ–º–ø–∏–ª–∏—Ä–æ–≤–∞—Ç—å –µ–≥–æ –≤–Ω–µ —Ü–µ–ø–æ—á–∫–∏ –≤ `cell` –∏ –æ—Ç–ø—Ä–∞–≤–∏—Ç—å —Å–æ–æ–±—â–µ–Ω–∏–µ –æ–± –æ–±–Ω–æ–≤–ª–µ–Ω–∏–∏ —Å–º–∞—Ä—Ç-–∫–æ–Ω—Ç—Ä–∞–∫—Ç—É `CounterV1`.

```javascript
await contractV1.sendUpgrade(provider.sender(), {
    code: await compile('ContractV2'),
    value: toNano('0.05'),
});
```

> üí° –ü–æ–ª–µ–∑–Ω—ã–µ —Å—Å—ã–ª–∫–∏
>
> [–í–æ–∑–º–æ–∂–Ω–æ –ª–∏ –ø–æ–≤—Ç–æ—Ä–Ω–æ —Ä–∞–∑–≤–µ—Ä–Ω—É—Ç—å –∫–æ–¥ –Ω–∞ —Å—É—â–µ—Å—Ç–≤—É—é—â–∏–π –∞–¥—Ä–µ—Å –∏–ª–∏ –µ–≥–æ –Ω—É–∂–Ω–æ —Ä–∞–∑–≤–µ—Ä–Ω—É—Ç—å –∫–∞–∫ –Ω–æ–≤—ã–π –∫–æ–Ω—Ç—Ä–∞–∫—Ç?](/v3/documentation/faq#is-it-possible-to-re-deploy-code-to-an-existing-address-or-does-it-have-to-be-deployed-as-a-new-contract)
>
> ["set_code()" –≤ –¥–æ–∫—É–º–µ–Ω—Ç–∞—Ü–∏–∏](/v3/documentation/smart-contracts/func/docs/stdlib#set_code)





