# FunC å¼€å‘æ‰‹å†Œ

åˆ›å»º FunC å¼€å‘æ‰‹å†Œçš„æ ¸å¿ƒåŸå› æ˜¯å°†æ‰€æœ‰ FunC å¼€å‘è€…çš„ç»éªŒæ±‡é›†åœ¨ä¸€ä¸ªåœ°æ–¹ï¼Œä»¥ä¾¿æœªæ¥çš„å¼€å‘è€…ä»¬ä½¿ç”¨ï¼

ä¸ FunC æ–‡æ¡£ç›¸æ¯”ï¼Œæœ¬æ–‡æ›´ä¾§é‡äº FunC å¼€å‘è€…åœ¨æ™ºèƒ½åˆçº¦å¼€å‘è¿‡ç¨‹ä¸­æ¯å¤©éƒ½è¦è§£å†³çš„ä»»åŠ¡ã€‚

## åŸºç¡€çŸ¥è¯†

### å¦‚ä½•ç¼–å†™ if è¯­å¥

å‡è®¾æˆ‘ä»¬æƒ³æ£€æŸ¥æŸä¸ªäº‹ä»¶æ˜¯å¦ç›¸å…³ã€‚ä¸ºæ­¤ï¼Œæˆ‘ä»¬ä½¿ç”¨æ ‡å¿—å˜é‡ã€‚è®°ä½åœ¨ FunC ä¸­ `true` æ˜¯ `-1` è€Œ `false` æ˜¯ `0`ã€‚

```func
int flag = 0; ;; false

if (flag) { 
    ;; do something
}
else {
    ;; reject the transaction
}
```

> ğŸ’¡ æ³¨æ„
>
> æˆ‘ä»¬ä¸éœ€è¦ä½¿ç”¨ `==` æ“ä½œç¬¦ï¼Œå› ä¸º `0` çš„å€¼æ˜¯ `false`ï¼Œæ‰€ä»¥ä»»ä½•å…¶ä»–å€¼éƒ½å°†æ˜¯ `true`ã€‚

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œIf statementâ€](/develop/func/statements#if-statements)

### å¦‚ä½•ç¼–å†™ repeat å¾ªç¯

ä»¥æŒ‡æ•°è¿ç®—ä¸ºä¾‹

```func
int number = 2;
int multiplier = number;
int degree = 5;

repeat(degree - 1) {

    number *= multiplier;
}
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œRepeat loopâ€](/develop/func/statements#repeat-loop)

### å¦‚ä½•ç¼–å†™ while å¾ªç¯

å½“æˆ‘ä»¬ä¸çŸ¥é“è¦æ‰§è¡Œç‰¹å®šæ“ä½œå¤šå°‘æ¬¡æ—¶ï¼Œwhile å¾ªç¯å¾ˆæœ‰ç”¨ã€‚ä¾‹å¦‚ï¼Œå–ä¸€ä¸ª `cell`ï¼Œæˆ‘ä»¬çŸ¥é“å®ƒå¯ä»¥å­˜å‚¨æœ€å¤šå››ä¸ªå¯¹å…¶ä»– cell çš„å¼•ç”¨ã€‚

```func
cell inner_cell = begin_cell() ;; create a new empty builder
        .store_uint(123, 16) ;; store uint with value 123 and length 16 bits
        .end_cell(); ;; convert builder to a cell

cell message = begin_cell()
        .store_ref(inner_cell) ;; store cell as reference
        .store_ref(inner_cell)
        .end_cell();

slice msg = message.begin_parse(); ;; convert cell to slice
while (msg.slice_refs_empty?() != -1) { ;; we should remind that -1 is true
    cell inner_cell = msg~load_ref(); ;; load cell from slice msg
    ;; do something
}
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œWhile loopâ€](/develop/func/statements#while-loop)
>
> [æ–‡æ¡£ä¸­çš„â€œCellâ€](/learn/overviews/cells)
>
> [æ–‡æ¡£ä¸­çš„â€œslice_refs_empty?()â€](/develop/func/stdlib#slice_refs_empty)
>
> [æ–‡æ¡£ä¸­çš„â€œstore_ref()â€](/develop/func/stdlib#store_ref)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_cell()â€](/develop/func/stdlib#begin_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œend_cell()â€](/develop/func/stdlib#end_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_parse()â€](/develop/func/stdlib#begin_parse)

### å¦‚ä½•ç¼–å†™ do until å¾ªç¯

å½“æˆ‘ä»¬éœ€è¦å¾ªç¯è‡³å°‘è¿è¡Œä¸€æ¬¡æ—¶ï¼Œæˆ‘ä»¬ä½¿ç”¨ `do until`ã€‚

```func
int flag = 0;

do {
    ;; do something even flag is false (0) 
} until (flag == -1); ;; -1 is true
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œUntil loopâ€](/develop/func/statements#until-loop)

### å¦‚ä½•ç¡®å®š slice æ˜¯å¦ä¸ºç©º

åœ¨å¤„ç† `slice` ä¹‹å‰ï¼Œéœ€è¦æ£€æŸ¥å®ƒæ˜¯å¦æœ‰æ•°æ®ä»¥ä¾¿æ­£ç¡®å¤„ç†ã€‚æˆ‘ä»¬å¯ä»¥ä½¿ç”¨ `slice_empty?()` æ¥åšåˆ°è¿™ä¸€ç‚¹ï¼Œä½†æˆ‘ä»¬å¿…é¡»è€ƒè™‘åˆ°ï¼Œå¦‚æœæœ‰è‡³å°‘ä¸€ä¸ª `bit` çš„æ•°æ®æˆ–ä¸€ä¸ª `ref`ï¼Œå®ƒå°†è¿”å› `-1`ï¼ˆ`true`ï¼‰ã€‚

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_empty?()` returns `true`, because slice doesn't have any `bits` and `refs`
empty_slice.slice_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_only.slice_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_empty?()` returns `false`, because slice have any `refs`
slice_with_refs_only.slice_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_empty?()` returns `false`, because slice have any `bits` and `refs`
slice_with_bits_and_refs.slice_empty?();
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œslice_empty?()â€](/develop/func/stdlib#slice_empty)
>
> [æ–‡æ¡£ä¸­çš„â€œstore_slice()â€](/develop/func/stdlib#store_slice)
>
> [æ–‡æ¡£ä¸­çš„â€œstore_ref()â€](/develop/func/stdlib#store_ref)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_cell()â€](/develop/func/stdlib#begin_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œend_cell()â€](/develop/func/stdlib#end_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_parse()â€](/develop/func/stdlib#begin_parse)

### å¦‚ä½•ç¡®å®š slice æ˜¯å¦ä¸ºç©ºï¼ˆä¸å«ä»»ä½• bitsï¼Œä½†å¯èƒ½åŒ…å« refsï¼‰

å¦‚æœæˆ‘ä»¬åªéœ€è¦æ£€æŸ¥ `bits`ï¼Œä¸å…³å¿ƒ `slice` ä¸­æ˜¯å¦æœ‰ä»»ä½• `refs`ï¼Œé‚£ä¹ˆæˆ‘ä»¬åº”è¯¥ä½¿ç”¨ `slice_data_empty?()`ã€‚

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_data_empty?()` returns `true`, because slice doesn't have any `bits`
empty_slice.slice_data_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_data_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_only.slice_data_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_data_empty?()` returns `true`, because slice doesn't have any `bits`
slice_with_refs_only.slice_data_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_data_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_and_refs.slice_data_empty?();
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œslice_data_empty?()â€](/develop/func/stdlib#slice_data_empty)
>
> [æ–‡æ¡£ä¸­çš„â€œstore_slice()â€](/develop/func/stdlib#store_slice)
>
> [æ–‡æ¡£ä¸­çš„â€œstore_ref()â€](/develop/func/stdlib#store_ref)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_cell()â€](/develop/func/stdlib#begin_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œend_cell()â€](/develop/func/stdlib#end_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_parse()â€](/develop/func/stdlib#begin_parse)

### å¦‚ä½•ç¡®å®š slice æ˜¯å¦ä¸ºç©ºï¼ˆæ²¡æœ‰ä»»ä½• refsï¼Œä½†å¯èƒ½æœ‰ bitsï¼‰

å¦‚æœæˆ‘ä»¬åªå¯¹ `refs` æ„Ÿå…´è¶£ï¼Œæˆ‘ä»¬åº”è¯¥ä½¿ç”¨ `slice_refs_empty?()` æ¥æ£€æŸ¥å®ƒä»¬çš„å­˜åœ¨ã€‚

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_refs_empty?()` returns `true`, because slice doesn't have any `refs`
empty_slice.slice_refs_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_refs_empty?()` returns `true`, because slice doesn't have any `refs`
slice_with_bits_only.slice_refs_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_refs_empty?()` returns `false`, because slice have any `refs`
slice_with_refs_only.slice_refs_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_refs_empty?()` returns `false`, because slice have any `refs`
slice_with_bits_and_refs.slice_refs_empty?();
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œslice_refs_empty?()â€](/develop/func/stdlib#slice_refs_empty)
>
> [æ–‡æ¡£ä¸­çš„â€œstore_slice()â€](/develop/func/stdlib#store_slice)
>
> [æ–‡æ¡£ä¸­çš„â€œstore_ref()â€](/develop/func/stdlib#store_ref)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_cell()â€](/develop/func/stdlib#begin_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œend_cell()â€](/develop/func/stdlib#end_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_parse()â€](/develop/func/stdlib#begin_parse)

### å¦‚ä½•ç¡®å®š cell æ˜¯å¦ä¸ºç©º

è¦æ£€æŸ¥ `cell` ä¸­æ˜¯å¦æœ‰ä»»ä½•æ•°æ®ï¼Œæˆ‘ä»¬åº”é¦–å…ˆå°†å…¶è½¬æ¢ä¸º `slice`ã€‚å¦‚æœæˆ‘ä»¬åªå¯¹ `bits` æ„Ÿå…´è¶£ï¼Œåº”ä½¿ç”¨ `slice_data_empty?()`ï¼›å¦‚æœåªå¯¹ `refs` æ„Ÿå…´è¶£ï¼Œåˆ™ä½¿ç”¨ `slice_refs_empty?()`ã€‚å¦‚æœæˆ‘ä»¬æƒ³æ£€æŸ¥æ˜¯å¦æœ‰ä»»ä½•æ•°æ®ï¼Œæ— è®ºæ˜¯ `bit` è¿˜æ˜¯ `ref`ï¼Œæˆ‘ä»¬éœ€è¦ä½¿ç”¨ `slice_empty?()`ã€‚

```func
cell cell_with_bits_and_refs = begin_cell()
    .store_uint(1337, 16)
    .store_ref(null())
    .end_cell();

;; Change `cell` type to slice with `begin_parse()`
slice cs = cell_with_bits_and_refs.begin_parse();

;; determine if slice is empty
if (cs.slice_empty?()) {
    ;; cell is empty
}
else {
    ;; cell is not empty
}
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œslice_empty?()â€](/develop/func/stdlib#slice_empty)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_cell()â€](/develop/func/stdlib#begin_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œstore_uint()â€](/develop/func/stdlib#store_uint)
>
> [æ–‡æ¡£ä¸­çš„â€œend_cell()â€](/develop/func/stdlib#end_cell)
>
> [æ–‡æ¡£ä¸­çš„â€œbegin_parse()â€](/develop/func/stdlib#begin_parse)

### å¦‚ä½•ç¡®å®š dict æ˜¯å¦ä¸ºç©º

æœ‰ä¸€ä¸ª `dict_empty?()` æ–¹æ³•å¯ä»¥æ£€æŸ¥ dict ä¸­æ˜¯å¦æœ‰æ•°æ®ã€‚è¿™ä¸ªæ–¹æ³•ç›¸å½“äº `cell_null?()`ï¼Œå› ä¸ºé€šå¸¸ä¸€ä¸ªç©ºçš„ cell å°±æ˜¯ä¸€ä¸ªç©ºå­—å…¸ã€‚

```func
cell d = new_dict();
d~udict_set(256, 0, "hello");
d~udict_set(256, 1, "world");

if (d.dict_empty?()) { ;; Determine if dict is empty
    ;; dict is empty
}
else {
    ;; dict is not empty
}
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œdict_empty?()â€](/develop/func/stdlib#dict_empty)
>
> [æ–‡æ¡£ä¸­çš„â€œnew_dict()â€](/develop/func/stdlib/#new_dict) åˆ›å»ºç©ºå­—å…¸
>
> [æ–‡æ¡£ä¸­çš„â€œdict_set()â€](/develop/

### å¦‚ä½•ç¡®å®š tuple æ˜¯å¦ä¸ºç©º

åœ¨å¤„ç† `tuple` æ—¶ï¼Œé‡è¦çš„æ˜¯å§‹ç»ˆçŸ¥é“é‡Œé¢æ˜¯å¦æœ‰ä»»ä½•å€¼å¯ä¾›æå–ã€‚å¦‚æœæˆ‘ä»¬è¯•å›¾ä»ä¸€ä¸ªç©ºçš„ "å…ƒç»„ "ä¸­æå–å€¼ï¼Œå°±ä¼šå‡ºç°é”™è¯¯ï¼šä¸æ˜¯æœ‰æ•ˆå¤§å°çš„å…ƒç»„"ï¼Œå¹¶æ˜¾ç¤º "é€€å‡ºä»£ç  7"ã€‚

```func
;; Declare tlen function because it's not presented in stdlib
(int) tlen (tuple t) asm "TLEN";

() main () {
    tuple t = empty_tuple();
    t~tpush(13);
    t~tpush(37);

    if (t.tlen() == 0) {
        ;; tuple is empty
    }
    else {
        ;; tuple is not empty
    }
}
```

> ğŸ’¡ Noted
>
> æˆ‘ä»¬æ­£åœ¨å£°æ˜ tlen æ±‡ç¼–å‡½æ•°ã€‚ä½ å¯ä»¥åœ¨ [æ­¤å¤„](/v3/documentation/smart-contracts/func/docs/functions#assembler-function-body-definition) å’Œ [list of all assembler commands](/v3/documentation/tvm/instructions) é˜…è¯»æ›´å¤šå†…å®¹ã€‚

> ğŸ’¡ æ³¨æ„
>
> æˆ‘ä»¬å£°æ˜äº† tlen æ±‡ç¼–å‡½æ•°ã€‚ä½ å¯ä»¥åœ¨[è¿™é‡Œ](/develop/func/functions#assembler-function-body-definition)é˜…è¯»æ›´å¤šï¼Œå¹¶æŸ¥çœ‹[æ‰€æœ‰æ±‡ç¼–æŒ‡ä»¤åˆ—è¡¨](/learn/tvm-instructions/instructions)ã€‚
>
> æ–‡æ¡£ä¸­çš„["tpush()"](/v3/documentation/smart-contracts/func/docs/stdlib/#tpush)
>
> [æ–‡æ¡£ä¸­çš„ "é€€å‡ºä»£ç "](/v3/documentation/tvm/tvm-exit-codes)

### å¦‚ä½•åˆ¤æ–­ lisp é£æ ¼åˆ—è¡¨æ˜¯å¦ä¸ºç©º

```func
tuple numbers = null();
numbers = cons(100, numbers);

if (numbers.null?()) {
    ;; list-style list is empty
} else {
    ;; list-style list is not empty
}
```

æˆ‘ä»¬ä½¿ç”¨ [cons](/v3/documentation/smart-contracts/func/docs/stdlib/#cons)å‡½æ•°å°†æ•°å­— 100 æ·»åŠ åˆ°åˆ—è¡¨æ ·å¼çš„åˆ—è¡¨ä¸­ï¼Œå› æ­¤å®ƒä¸æ˜¯ç©ºçš„ã€‚

### å¦‚ä½•ç¡®å®šåˆçº¦çŠ¶æ€ä¸ºç©º

å‡è®¾æˆ‘ä»¬æœ‰ä¸€ä¸ªå­˜å‚¨äº¤æ˜“æ•°é‡çš„ `counter`ã€‚åœ¨æ™ºèƒ½åˆçº¦çŠ¶æ€ä¸‹çš„ç¬¬ä¸€ç¬”äº¤æ˜“ä¸­ï¼Œè¿™ä¸ªå˜é‡æ˜¯ä¸å¯ç”¨çš„ï¼Œå› ä¸ºçŠ¶æ€æ˜¯ç©ºçš„ï¼Œæ‰€ä»¥æœ‰å¿…è¦å¤„ç†è¿™ç§æƒ…å†µã€‚å¦‚æœçŠ¶æ€ä¸ºç©ºï¼Œæˆ‘ä»¬å°±åˆ›å»ºä¸€ä¸ªå˜é‡ `counter` å¹¶ä¿å­˜å®ƒã€‚

```func
;; `get_data()` will return the data cell from contract state
cell contract_data = get_data();
slice cs = contract_data.begin_parse();

if (cs.slice_empty?()) {
    ;; contract data is empty, so we create counter and save it
    int counter = 1;
    ;; create cell, add counter and save in contract state
    set_data(begin_cell().store_uint(counter, 32).end_cell());
}
else {
    ;; contract data is not empty, so we get our counter, increase it and save
    ;; we should specify correct length of our counter in bits
    int counter = cs~load_uint(32) + 1;
    set_data(begin_cell().store_uint(counter, 32).end_cell());
}
```

> ğŸ’¡ Noted
>
> æˆ‘ä»¬å¯ä»¥é€šè¿‡åˆ¤æ–­ [cell is empty](/v3/documentation/smart-contracts/func/cookbook#how-to-determine-if-cell-is-empty) æ¥ç¡®å®šåˆçº¦çŠ¶æ€ä¸ºç©ºã€‚

> ğŸ’¡ æ³¨æ„
>
> æˆ‘ä»¬å¯ä»¥é€šè¿‡ç¡®å®š [cell æ˜¯å¦ä¸ºç©º](/develop/func/cookbook#how-to-determine-if-cell-is-empty) æ¥ç¡®å®šåˆçº¦çš„çŠ¶æ€æ˜¯å¦ä¸ºç©ºã€‚
>
> æ–‡æ¡£ä¸­çš„["begin_parse()"](/v3/documentation/smart-contracts/func/docs/stdlib/#begin_parse)
>
> æ–‡æ¡£ä¸­çš„ ["slice_empty?()"](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_empty)
>
> æ–‡æ¡£ä¸­çš„ ["set_data?()"](/v3/documentation/smart-contracts/func/docs/stdlib#set_data)

### å¦‚ä½•å»ºç«‹å†…éƒ¨ä¿¡æ¯ cell

å¦‚æœæˆ‘ä»¬æƒ³è®©åˆçº¦å‘é€å†…éƒ¨é‚®ä»¶ï¼Œé¦–å…ˆåº”å°†å…¶åˆ›å»ºä¸º cell ï¼Œå¹¶æŒ‡å®šæŠ€æœ¯æ ‡å¿—ã€æ”¶ä»¶äººåœ°å€å’Œå…¶ä»–æ•°æ®ã€‚

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
;; we use `op` for identifying operations
int op = 0;

cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(op, 32)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> ğŸ’¡ Noted
>
> åœ¨æœ¬ä¾‹ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨å­—é¢é‡ `a` æ¥è·å–åœ°å€ã€‚æœ‰å…³å­—ç¬¦ä¸²å­—é¢é‡çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚é˜… [docs](/v3/documentation/smart-contracts/func/docs/literals_identifiers#string-literals)

> ğŸ’¡ æ³¨æ„
>
> åœ¨è¿™ä¸ªä¾‹å­ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨å­—é¢é‡ `a` è·å–åœ°å€ã€‚ä½ å¯ä»¥åœ¨[æ–‡æ¡£](/develop/func/literals_identifiers#string-literals)ä¸­æ‰¾åˆ°æ›´å¤šå…³äºå­—ç¬¦ä¸²å­—é¢é‡çš„ä¿¡æ¯ã€‚

> ğŸ’¡ æ³¨æ„
>
> ä½ å¯ä»¥åœ¨[æ–‡æ¡£](/develop/smart-contracts/messages)ä¸­æ‰¾åˆ°æ›´å¤šä¿¡æ¯ã€‚ä¹Ÿå¯ä»¥é€šè¿‡è¿™ä¸ªé“¾æ¥è·³è½¬åˆ°[å¸ƒå±€](/develop/smart-contracts/messages#message-layout)ã€‚
>
> [æ–‡æ¡£ä¸­çš„ "store_uint() "](/v3/documentation/smart-contracts/func/docs/stdlib#store_uint)
>
> [æ–‡æ¡£ä¸­çš„ "store_slice() "](/v3/documentation/smart-contracts/func/docs/stdlib#store_slice)
>
> [æ–‡æ¡£ä¸­çš„ "store_coins()"](/v3/documentation/smart-contracts/func/docs/stdlib#store_coins)
>
> [æ–‡æ¡£ä¸­çš„ "end_cell()"](/v3/documentation/smart-contracts/func/docs/stdlib/#end_cell)
>
> [æ–‡æ¡£ä¸­çš„ "send_raw_message() "](/v3/documentation/smart-contracts/func/docs/stdlib/#send_raw_message)

### å¦‚ä½•å°†æ­£æ–‡ä½œä¸ºå†…éƒ¨æŠ¥æ–‡ cell çš„ ref æ¥åŒ…å«

åœ¨æ ‡å¿—å’Œå…¶ä»–æŠ€æœ¯æ•°æ®ä¹‹åçš„æŠ¥æ–‡æ­£æ–‡ä¸­ï¼Œæˆ‘ä»¬å¯ä»¥å‘é€ `int`, `slice` å’Œ `cell`ã€‚å¯¹äºåè€…ï¼Œæœ‰å¿…è¦åœ¨ `store_ref()` ä¹‹å‰å°†ä½è®¾ç½®ä¸º `1`ï¼Œä»¥è¡¨ç¤º `cell` å°†ç»§ç»­ã€‚

åœ¨è·Ÿç€æ ‡å¿—ä½å’Œå…¶ä»–æŠ€æœ¯æ•°æ®çš„æ¶ˆæ¯ä½“ä¸­ï¼Œæˆ‘ä»¬å¯ä»¥å‘é€ `int`ã€`slice` å’Œ `cell`ã€‚åœ¨åè€…çš„æƒ…å†µä¸‹ï¼Œåœ¨ `store_ref()` ä¹‹å‰å¿…é¡»å°†ä½è®¾ç½®ä¸º `1`ï¼Œä»¥è¡¨æ˜ `cell` å°†ç»§ç»­ä¼ è¾“ã€‚

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
int op = 0;
cell message_body = begin_cell() ;; Creating a cell with message
    .store_uint(op, 32)
    .store_slice("â¤")
.end_cell();
    
cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1) ;; default message headers (see sending messages page)
    .store_uint(1, 1) ;; set bit to 1 to indicate that the cell will go on
    .store_ref(message_body)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> ğŸ’¡ Noted
>
> åœ¨æœ¬ä¾‹ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨å­—é¢é‡ `a` æ¥è·å–åœ°å€ã€‚æœ‰å…³å­—ç¬¦ä¸²å­—é¢é‡çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚é˜… [docs](/v3/documentation/smart-contracts/func/docs/literals_identifiers#string-literals)

> ğŸ’¡ æ³¨æ„
>
> åœ¨è¿™ä¸ªä¾‹å­ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨å­—é¢é‡ `a` è·å–åœ°å€ã€‚ä½ å¯ä»¥åœ¨[æ–‡æ¡£](/develop/func/literals_identifiers#string-literals)ä¸­æ‰¾åˆ°æ›´å¤šå…³äºå­—ç¬¦ä¸²å­—é¢é‡çš„ä¿¡æ¯ã€‚

> ğŸ’¡ æ³¨æ„
>
> åœ¨è¿™ä¸ªä¾‹å­ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨node 3 æ¥æ”¶è¿›æ¥çš„ tons å¹¶å‘é€ç¡®åˆ‡çš„æŒ‡å®šé‡‘é¢ï¼ˆamountï¼‰ï¼ŒåŒæ—¶ä»åˆçº¦ä½™é¢ä¸­æ”¯ä»˜ä½£é‡‘å¹¶å¿½ç•¥é”™è¯¯ã€‚mode 64 ç”¨äºè¿”å›æ‰€æœ‰æ¥æ”¶åˆ°çš„ tonsï¼Œæ‰£é™¤ä½£é‡‘ï¼Œmode 128 å°†å‘é€æ•´ä¸ªä½™é¢ã€‚

> ğŸ’¡ æ³¨æ„
>
> æˆ‘ä»¬æ­£åœ¨[æ„å»ºæ¶ˆæ¯](/develop/func/cookbook#how-to-build-an-internal-message-cell)ï¼Œä½†å•ç‹¬æ·»åŠ æ¶ˆæ¯ä½“ã€‚
>
> [æ–‡æ¡£ä¸­çš„ "store_uint() "](/v3/documentation/smart-contracts/func/docs/stdlib#store_uint)
>
> [æ–‡æ¡£ä¸­çš„ "store_slice() "](/v3/documentation/smart-contracts/func/docs/stdlib#store_slice)
>
> [æ–‡æ¡£ä¸­çš„ "store_coins()"](/v3/documentation/smart-contracts/func/docs/stdlib#store_coins)
>
> [æ–‡æ¡£ä¸­çš„ "end_cell()"](/v3/documentation/smart-contracts/func/docs/stdlib/#end_cell)
>
> [æ–‡æ¡£ä¸­çš„ "send_raw_message() "](/v3/documentation/smart-contracts/func/docs/stdlib/#send_raw_message)

### å¦‚ä½•å°†æ­£æ–‡ä½œä¸ºç‰‡æ®µåŒ…å«åœ¨å†…éƒ¨æŠ¥æ–‡ cell ä¸­

å‘é€ä¿¡æ¯æ—¶ï¼Œä¿¡æ¯æ­£æ–‡å¯ä»¥ä½œä¸º `cell` æˆ– `slice` å‘é€ã€‚åœ¨æœ¬ä¾‹ä¸­ï¼Œæˆ‘ä»¬åœ¨ `slice` å†…å‘é€æ­£æ–‡ä¿¡æ¯ã€‚

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
int op = 0;
slice message_body = "â¤"; 

cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(op, 32)
    .store_slice(message_body)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> ğŸ’¡ Noted
>
> åœ¨æœ¬ä¾‹ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨å­—é¢é‡ `a` æ¥è·å–åœ°å€ã€‚æœ‰å…³å­—ç¬¦ä¸²å­—é¢é‡çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚é˜… [docs](/v3/documentation/smart-contracts/func/docs/literals_identifiers#string-literals)

> ğŸ’¡ æ³¨æ„
>
> åœ¨è¿™ä¸ªä¾‹å­ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨å­—é¢é‡ `a` è·å–åœ°å€ã€‚ä½ å¯ä»¥åœ¨[æ–‡æ¡£](/develop/func/literals_identifiers#string-literals)ä¸­æ‰¾åˆ°æ›´å¤šå…³äºå­—ç¬¦ä¸²å­—é¢é‡çš„ä¿¡æ¯ã€‚

> ğŸ’¡ æ³¨æ„
>
> åœ¨è¿™ä¸ªä¾‹å­ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨ mode 3 æ¥æ”¶è¿›æ¥çš„ tons å¹¶å‘é€ç¡®åˆ‡çš„æŒ‡å®šé‡‘é¢ï¼ˆamountï¼‰ï¼ŒåŒæ—¶ä»åˆçº¦ä½™é¢ä¸­æ”¯ä»˜ä½£é‡‘å¹¶å¿½ç•¥é”™è¯¯ã€‚mode 64 ç”¨äºè¿”å›æ‰€æœ‰æ¥æ”¶åˆ°çš„ tonsï¼Œæ‰£é™¤ä½£é‡‘ï¼Œmode 128 å°†å‘é€æ•´ä¸ªä½™é¢ã€‚

### å¦‚ä½•è¿­ä»£ tuplesï¼ˆåŒå‘ï¼‰

å¦‚æœæˆ‘ä»¬æƒ³åœ¨ FunC ä¸­å¤„ç†æ•°ç»„æˆ–å †æ ˆï¼Œé‚£ä¹ˆ tuple å°±æ˜¯å¿…è¦çš„ã€‚é¦–å…ˆï¼Œæˆ‘ä»¬éœ€è¦èƒ½å¤Ÿéå†å€¼æ¥å¤„ç†å®ƒä»¬ã€‚

```func
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

() main () {
    tuple t = to_tuple([1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
    int len = t.tlen();
    
    int i = 0;
    while (i < len) {
        int x = t.at(i);
        ;; do something with x
        i = i + 1;
    }

    i = len - 1;
    while (i >= 0) {
        int x = t.at(i);
        ;; do something with x
        i = i - 1;
    }
}
```

> ğŸ’¡ Noted
>
> æˆ‘ä»¬æ­£åœ¨å£°æ˜ `tlen` æ±‡ç¼–å‡½æ•°ã€‚æ‚¨å¯ä»¥ [åœ¨æ­¤](/v3/documentation/smart-contracts/func/docs/functions#assembler-function-body-definition) é˜…è¯»æ›´å¤šå†…å®¹ï¼Œä¹Ÿå¯ä»¥æŸ¥çœ‹ [æ‰€æœ‰æ±‡ç¼–å‘½ä»¤åˆ—è¡¨](/v3/documentation/tvm/instructions)ã€‚
>
> æˆ‘ä»¬è¿˜å£°æ˜äº† `to_tuple` å‡½æ•°ã€‚å®ƒåªæ˜¯å°†ä»»ä½•è¾“å…¥çš„æ•°æ®ç±»å‹æ›´æ”¹ä¸ºå…ƒç»„ï¼Œå› æ­¤ä½¿ç”¨æ—¶è¦å°å¿ƒã€‚

### å¦‚ä½•ä½¿ç”¨ `asm` å…³é”®å­—ç¼–å†™è‡ªå·±çš„å‡½æ•°

åœ¨ä½¿ç”¨ä»»ä½•åŠŸèƒ½æ—¶ï¼Œæˆ‘ä»¬å®é™…ä¸Šä½¿ç”¨çš„æ˜¯ `stdlib.fc` å†…ä¸ºæˆ‘ä»¬é¢„å…ˆå‡†å¤‡å¥½çš„æ–¹æ³•ã€‚ä½†äº‹å®ä¸Šï¼Œæˆ‘ä»¬è¿˜æœ‰æ›´å¤šçš„æœºä¼šï¼Œæˆ‘ä»¬éœ€è¦å­¦ä¼šè‡ªå·±ç¼–å†™ã€‚

å½“ä½¿ç”¨ä»»ä½•åŠŸèƒ½æ—¶ï¼Œå®é™…ä¸Šæˆ‘ä»¬ä½¿ç”¨çš„æ˜¯ä¸ºæˆ‘ä»¬é¢„å…ˆå‡†å¤‡å¥½çš„ `stdlib.fc` ä¸­çš„æ–¹æ³•ã€‚ä½†äº‹å®ä¸Šï¼Œæˆ‘ä»¬æœ‰æ›´å¤šçš„æœºä¼šå¯ä»¥ä½¿ç”¨ï¼Œæˆ‘ä»¬éœ€è¦å­¦ä¼šè‡ªå·±ç¼–å†™å®ƒä»¬ã€‚

```func
;; ~ means it is modifying method
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP"; 
```

å¦‚æœæˆ‘ä»¬æƒ³çŸ¥é“ç”¨äºè¿­ä»£çš„ "å…ƒç»„ "çš„é•¿åº¦ï¼Œåˆ™åº”ä½¿ç”¨ `TLEN` asm æŒ‡ä»¤ç¼–å†™ä¸€ä¸ªæ–°å‡½æ•°ï¼š

```func
int tuple_length (tuple t) asm "TLEN";
```

ä¸€äº›æˆ‘ä»¬å·²ç»ä» stdlib.fc ä¸­äº†è§£åˆ°çš„å‡½æ•°ç¤ºä¾‹ï¼š

```func
slice begin_parse(cell c) asm "CTOS";
builder begin_cell() asm "NEWC";
cell end_cell(builder b) asm "ENDC";
```

> ğŸ’¡ Useful links:
>
> [æ–‡æ¡£ä¸­çš„ "ä¿®æ”¹æ–¹æ³•"](/v3/documentation/smart-contracts/func/docs/statements#modifying-methods)
>
> [æ–‡æ¡£ä¸­çš„ "stdlib"](/v3/documentation/smart-contracts/func/docs/stdlib)
>
> [æ–‡æ¡£ä¸­çš„ "TVM è¯´æ˜"](/v3/documentation/tvm/instructions)

### è¿­ä»£åµŒå¥—çš„ n ä¸ª tuples

æœ‰æ—¶æˆ‘ä»¬æƒ³è¿­ä»£åµŒå¥—çš„ tuplesã€‚ä»¥ä¸‹ç¤ºä¾‹å°†ä»å¤´å¼€å§‹è¿­ä»£å¹¶æ‰“å°æ ¼å¼ä¸º `[[2,6],[1,[3,[3,5]]], 3]` çš„ tuple ä¸­çš„æ‰€æœ‰é¡¹ç›®

```func
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
forall X -> int is_tuple (X x) asm "ISTUPLE";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> (tuple) to_tuple (X x) asm "NOP";

;; define global variable
global int max_value;

() iterate_tuple (tuple t) impure {
    repeat (t.tuple_length()) {
        var value = t~tpop();
        if (is_tuple(value)) {
            tuple tuple_value = cast_to_tuple(value);
            iterate_tuple(tuple_value);
        }
        else {
            if(value > max_value) {
                max_value = value;
            }
        }
    }
}

() main () {
    tuple t = to_tuple([[2,6], [1, [3, [3, 5]]], 3]);
    int len = t.tuple_length();
    max_value = 0; ;; reset max_value;
    iterate_tuple(t); ;; iterate tuple and find max value
    ~dump(max_value); ;; 6
}
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "å…¨å±€å˜é‡"](/v3/documentation/smart-contracts/func/docs/global_variables)
>
> [æ–‡æ¡£ä¸­çš„"~dump"](/v3/documentation/smart-contracts/func/docs/builtins#dump-variable)
>
> [æ–‡æ¡£ä¸­çš„ "TVM è¯´æ˜"](/v3/documentation/tvm/instructions)

### åŸºæœ¬çš„ tuple æ“ä½œ

```func
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

() main () {
    ;; creating an empty tuple
    tuple names = empty_tuple(); 
    
    ;; push new items
    names~tpush("Naito Narihira");
    names~tpush("Shiraki Shinichi");
    names~tpush("Akamatsu Hachemon");
    names~tpush("Takaki Yuichi");
    
    ;; pop last item
    slice last_name = names~tpop();

    ;; get first item
    slice first_name = names.first();

    ;; get an item by index
    slice best_name = names.at(2);

    ;; getting the length of the list 
    int number_names = names.tlen();
}
```

### è§£å†³ X ç±»é—®é¢˜

ä¸‹é¢çš„ç¤ºä¾‹æ£€æŸ¥å…ƒç»„ä¸­æ˜¯å¦åŒ…å«æŸäº›å€¼ï¼Œä½†å…ƒç»„åŒ…å«å€¼ Xï¼ˆ cell ã€sliceã€intã€tupleã€intï¼‰ã€‚æˆ‘ä»¬éœ€è¦æ£€æŸ¥å€¼å¹¶è¿›è¡Œç›¸åº”çš„è½¬æ¢ã€‚

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> int is_int (X x) asm "<{ TRY:<{ 0 PUSHINT ADD DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_cell (X x) asm "<{ TRY:<{ CTOS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_slice (X x) asm "<{ TRY:<{ SBITS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_tuple (X x) asm "ISTUPLE";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> cell cast_to_cell (X x) asm "NOP";
forall X -> slice cast_to_slice (X x) asm "NOP";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

forall X -> () resolve_type (X value) impure {
    ;; value here is of type X, since we dont know what is the exact value - we would need to check what is the value and then cast it
    
    if (is_null(value)) {
        ;; do something with the null
    }
    elseif (is_int(value)) {
        int valueAsInt = cast_to_int(value);
        ;; do something with the int
    }
    elseif (is_slice(value)) {
        slice valueAsSlice = cast_to_slice(value);
        ;; do something with the slice
    }
    elseif (is_cell(value)) {
        cell valueAsCell = cast_to_cell(value);
        ;; do something with the cell
    }
    elseif (is_tuple(value)) {
        tuple valueAsTuple = cast_to_tuple(value);
        ;; do something with the tuple
    }
}

() main () {
    ;; creating an empty tuple
    tuple stack = empty_tuple();
    ;; let's say we have tuple and do not know the exact types of them
    stack~tpush("Some text");
    stack~tpush(4);
    ;; we use var because we do not know type of value
    var value = stack~tpop();
    resolve_type(value);
}
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "TVM è¯´æ˜"](/v3/documentation/tvm/instructions)

### å¦‚ä½•è·å–å½“å‰æ—¶é—´

```func
int current_time = now();
  
if (current_time > 1672080143) {
    ;; do some stuff 
}
```

### å¦‚ä½•ç”Ÿæˆéšæœºæ•°

:::caution è‰æ¡ˆ

æ›´å¤šä¿¡æ¯è¯·æŸ¥é˜… [éšæœºæ•°ç”Ÿæˆ](/v3/guidelines/smart-contracts/security/random-number-generation)ã€‚
:::

```func
randomize_lt(); ;; do this once

int a = rand(10);
int b = rand(1000000);
int c = random();
```

### æ¨¡æ•°è¿ç®—

ä¾‹å¦‚ï¼Œæˆ‘ä»¬è¦å¯¹æ‰€æœ‰ 256 ä¸ªæ•°å­—è¿›è¡Œå¦‚ä¸‹è®¡ç®—ï¼š`(xp + zp)*(xp-zp)` ã€‚ç”±äºè¿™äº›è¿ç®—å¤§å¤šç”¨äºå¯†ç å­¦ï¼Œå› æ­¤åœ¨ä¸‹é¢çš„ç¤ºä¾‹ä¸­ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨è’™æ‰˜å“¥é©¬åˆ©æ›²çº¿çš„æ¨¡è¿ç®—ç¬¦ã€‚
è¯·æ³¨æ„ï¼Œxp+zp æ˜¯ä¸€ä¸ªæœ‰æ•ˆçš„å˜é‡åï¼ˆä¸­é—´æ²¡æœ‰ç©ºæ ¼ï¼‰ã€‚

```func
(int) modulo_operations (int xp, int zp) {  
   ;; 2^255 - 19 is a prime number for montgomery curves, meaning all operations should be done against its prime
   int prime = 57896044618658097711785492504343953926634992332820282019728792003956564819949; 

   ;; muldivmod handles the next two lines itself
   ;; int xp+zp = (xp + zp) % prime;
   ;; int xp-zp = (xp - zp + prime) % prime;
   (_, int xp+zp*xp-zp) = muldivmod(xp + zp, xp - zp, prime);
   return xp+zp*xp-zp;
}
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "muldivmod"](/v3/documentation/tvm/instructions#A98C)

### å¦‚ä½•æŠ›å‡ºé”™è¯¯

```func
int number = 198;

throw_if(35, number > 50); ;; the error will be triggered only if the number is greater than 50

throw_unless(39, number == 198); ;; the error will be triggered only if the number is NOT EQUAL to 198

throw(36); ;; the error will be triggered anyway
```

[æ ‡å‡† tvm å¼‚å¸¸ä»£ç ](/v3/documentation/tvm/tvm-exit-codes)

### åè½¬ tuples

ç”±äº tuple ä»¥å †æ ˆçš„å½¢å¼å­˜å‚¨æ•°æ®ï¼Œæœ‰æ—¶æˆ‘ä»¬å¿…é¡»åè½¬ tuple æ‰èƒ½ä»å¦ä¸€ç«¯è¯»å–æ•°æ®ã€‚

```func
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

(tuple) reverse_tuple (tuple t1) {
    tuple t2 = empty_tuple();
    repeat (t1.tuple_length()) {
        var value = t1~tpop();
        t2~tpush(value);
    }
    return t2;
}

() main () {
    tuple t = to_tuple([1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
    tuple reversed_t = reverse_tuple(t);
    ~dump(reversed_t); ;; [10 9 8 7 6 5 4 3 2 1]
}
```

> ğŸ’¡ Useful links
>
> æ–‡æ¡£ä¸­çš„["tpush()"](/v3/documentation/smart-contracts/func/docs/stdlib/#tpush)

### å¦‚ä½•ä»åˆ—è¡¨ä¸­åˆ é™¤å…·æœ‰ç‰¹å®šç´¢å¼•çš„é¡¹ç›®

```func
int tlen (tuple t) asm "TLEN";

(tuple, ()) remove_item (tuple old_tuple, int place) {
    tuple new_tuple = empty_tuple();

    int i = 0;
    while (i < old_tuple.tlen()) {
        int el = old_tuple.at(i);
        if (i != place) {
            new_tuple~tpush(el);
        }
        i += 1;  
    }
    return (new_tuple, ());
}

() main () {
    tuple numbers = empty_tuple();

    numbers~tpush(19);
    numbers~tpush(999);
    numbers~tpush(54);

    ~dump(numbers); ;; [19 999 54]

    numbers~remove_item(1); 

    ~dump(numbers); ;; [19 54]
}
```

### ç¡®å®š slice æ˜¯å¦ç›¸ç­‰

æˆ‘ä»¬æœ‰ä¸¤ç§ä¸åŒçš„æ–¹æ³•æ¥ç¡®å®šç›¸ç­‰æ€§ã€‚ä¸€ç§æ˜¯åŸºäº slice æ•£åˆ—ï¼Œå¦ä¸€ç§æ˜¯ä½¿ç”¨ SDEQ asm æŒ‡ä»¤ã€‚

```func
int are_slices_equal_1? (slice a, slice b) {
    return a.slice_hash() == b.slice_hash();
}

int are_slices_equal_2? (slice a, slice b) asm "SDEQ";

() main () {
    slice a = "Some text";
    slice b = "Some text";
    ~dump(are_slices_equal_1?(a, b)); ;; -1 = true

    a = "Text";
    ;; We use literal `a` to get valid address inside slice from string containing address
    b = "EQDKbjIcfM6ezt8KjKJJLshZJJSqX7XOA4ff-W72r5gqPrHF"a;
    ~dump(are_slices_equal_2?(a, b)); ;; 0 = false
}
```

#### åˆ¤æ–­cellæ˜¯å¦ç›¸ç­‰

- æ–‡æ¡£ä¸­çš„["slice_hash()"](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_hash)
- [æ–‡æ¡£ä¸­çš„ "SDEQ"](/v3/documentation/tvm/instructions#C705)

### ç¡®å®š cell æ˜¯å¦ç›¸ç­‰

æˆ‘ä»¬å¯ä»¥æ ¹æ®å“ˆå¸Œå€¼è½»æ¾ç¡®å®š cell æ˜¯å¦ç›¸ç­‰ã€‚

```func
int are_cells_equal? (cell a, cell b) {
    return a.cell_hash() == b.cell_hash();
}

() main () {
    cell a = begin_cell()
            .store_uint(123, 16)
            .end_cell();

    cell b = begin_cell()
            .store_uint(123, 16)
            .end_cell();

    ~dump(are_cells_equal?(a, b)); ;; -1 = true
}
```

> ğŸ’¡ Useful links
>
> docs ä¸­çš„["cell_hash()"](/v3/documentation/smart-contracts/func/docs/stdlib/#cell_hash)

### ç¡®å®š tuples æ˜¯å¦ç›¸ç­‰

æ›´é«˜çº§çš„ç¤ºä¾‹æ˜¯éå†å’Œæ¯”è¾ƒæ¯ä¸ª tuple å€¼ã€‚ç”±äºå®ƒä»¬éƒ½æ˜¯ Xï¼Œå› æ­¤æˆ‘ä»¬éœ€è¦æ£€æŸ¥å¹¶è½¬æ¢ä¸ºç›¸åº”çš„ç±»å‹ï¼Œå¦‚æœæ˜¯tupleï¼Œåˆ™è¿›è¡Œé€’å½’éå†ã€‚

```func
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> cell cast_to_cell (X x) asm "NOP";
forall X -> slice cast_to_slice (X x) asm "NOP";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> int is_null (X x) asm "ISNULL";
forall X -> int is_int (X x) asm "<{ TRY:<{ 0 PUSHINT ADD DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_cell (X x) asm "<{ TRY:<{ CTOS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_slice (X x) asm "<{ TRY:<{ SBITS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_tuple (X x) asm "ISTUPLE";
int are_slices_equal? (slice a, slice b) asm "SDEQ";

int are_cells_equal? (cell a, cell b) {
    return a.cell_hash() == b.cell_hash();
}

(int) are_tuples_equal? (tuple t1, tuple t2) {
    int equal? = -1; ;; initial value to true
    
    if (t1.tuple_length() != t2.tuple_length()) {
        ;; if tuples are differ in length they cannot be equal
        return 0;
    }

    int i = t1.tuple_length();
    
    while (i > 0 & equal?) {
        var v1 = t1~tpop();
        var v2 = t2~tpop();
        
        if (is_null(t1) & is_null(t2)) {
            ;; nulls are always equal
        }
        elseif (is_int(v1) & is_int(v2)) {
            if (cast_to_int(v1) != cast_to_int(v2)) {
                equal? = 0;
            }
        }
        elseif (is_slice(v1) & is_slice(v2)) {
            if (~ are_slices_equal?(cast_to_slice(v1), cast_to_slice(v2))) {
                equal? = 0;
            }
        }
        elseif (is_cell(v1) & is_cell(v2)) {
            if (~ are_cells_equal?(cast_to_cell(v1), cast_to_cell(v2))) {
                equal? = 0;
            }
        }
        elseif (is_tuple(v1) & is_tuple(v2)) {
            ;; recursively determine nested tuples
            if (~ are_tuples_equal?(cast_to_tuple(v1), cast_to_tuple(v2))) {
                equal? = 0;
            }
        }
        else {
            equal? = 0;
        }

        i -= 1;
    }

    return equal?;
}

() main () {
    tuple t1 = cast_to_tuple([[2, 6], [1, [3, [3, 5]]], 3]);
    tuple t2 = cast_to_tuple([[2, 6], [1, [3, [3, 5]]], 3]);

    ~dump(are_tuples_equal?(t1, t2)); ;; -1 
}
```

> ğŸ’¡ Useful links
>
> docs ä¸­çš„["cell_hash()"](/v3/documentation/smart-contracts/func/docs/stdlib/#cell_hash)
>
> [æ–‡æ¡£ä¸­çš„ "TVM è¯´æ˜"](/v3/documentation/tvm/instructions)

### ç”Ÿæˆå†…éƒ¨åœ°å€

å½“æˆ‘ä»¬çš„åˆçº¦éœ€è¦éƒ¨ç½²ä¸€ä¸ªæ–°åˆçº¦ï¼Œä½†ä¸çŸ¥é“ä»–çš„åœ°å€æ—¶ï¼Œæˆ‘ä»¬éœ€è¦ç”Ÿæˆä¸€ä¸ªå†…éƒ¨åœ°å€ã€‚å‡è®¾æˆ‘ä»¬å·²ç»æœ‰äº† `state_init` - æ–°åˆçº¦çš„ä»£ç å’Œæ•°æ®ã€‚

ä¸ºç›¸åº”çš„ MsgAddressInt TLB åˆ›å»ºå†…éƒ¨åœ°å€ã€‚

```func
(slice) generate_internal_address (int workchain_id, cell state_init) {
    ;; addr_std$10 anycast:(Maybe Anycast) workchain_id:int8 address:bits256  = MsgAddressInt;

    return begin_cell()
        .store_uint(2, 2) ;; addr_std$10
        .store_uint(0, 1) ;; anycast nothing
        .store_int(workchain_id, 8) ;; workchain_id: -1
        .store_uint(cell_hash(state_init), 256)
    .end_cell().begin_parse();
}

() main () {
    slice deploy_address = generate_internal_address(workchain(), state_init);
    ;; then we can deploy new contract
}
```

> ğŸ’¡ Noted
>
> åœ¨æœ¬ä¾‹ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨ `workchain()` è·å–å·¥ä½œé“¾çš„ IDã€‚æœ‰å…³å·¥ä½œé“¾ ID çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚é˜… [docs](/v3/documentation/smart-contracts/addresses#workchain-id) ã€‚

> ğŸ’¡ Useful links
>
> docs ä¸­çš„["cell_hash()"](/v3/documentation/smart-contracts/func/docs/stdlib/#cell_hash)

### ç”Ÿæˆå¤–éƒ¨åœ°å€

ç”±äºæˆ‘ä»¬éœ€è¦ç¡®å®šåœ°å€å ç”¨çš„ä½æ•°ï¼Œå› æ­¤è¿˜éœ€è¦[å£°æ˜ä¸€ä¸ªä½¿ç”¨ `UBITSIZE` æ“ä½œç çš„ asm å‡½æ•°](#how-to-write-own-functions-using-asm-keyword)ï¼Œè¯¥å‡½æ•°å°†è¿”å›å­˜å‚¨æ•°å­—æ‰€éœ€çš„æœ€å°ä½æ•°ã€‚

```func
(int) ubitsize (int a) asm "UBITSIZE";

slice generate_external_address (int address) {
    ;; addr_extern$01 len:(## 9) external_address:(bits len) = MsgAddressExt;
    
    int address_length = ubitsize(address);
    
    return begin_cell()
        .store_uint(1, 2) ;; addr_extern$01
        .store_uint(address_length, 9)
        .store_uint(address, address_length)
    .end_cell().begin_parse();
}
```

ç”±äºæˆ‘ä»¬éœ€è¦ç¡®å®šåœ°å€æ‰€å çš„ä½æ•°ï¼Œå› æ­¤è¿˜éœ€è¦ [å£°æ˜ä¸€ä¸ª asm å‡½æ•°](#how-to-write-own-functions-using-asm-keyword)ï¼Œå¹¶ä½¿ç”¨æ“ä½œç  `UBITSIZE` è¿”å›å­˜å‚¨æ•°å­—æ‰€éœ€çš„æœ€å°ä½æ•°ã€‚

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "TVM è¯´æ˜"](/v3/documentation/tvm/instructions#B603)

### å¦‚ä½•åœ¨æœ¬åœ°å­˜å‚¨ä¸­å­˜å‚¨å’ŒåŠ è½½å­—å…¸

è€Œå­˜å‚¨å­—å…¸çš„é€»è¾‘å¦‚ä¸‹æ‰€ç¤ºï¼š

```func
slice local_storage = get_data().begin_parse();
cell dictionary_cell = new_dict();
if (~ slice_empty?(local_storage)) {
    dictionary_cell = local_storage~load_dict();
}
```

è€Œå­˜å‚¨å­—å…¸çš„é€»è¾‘å°±åƒä¸‹é¢çš„ä¾‹å­ä¸€æ ·ï¼š

```func
set_data(begin_cell().store_dict(dictionary_cell).end_cell());
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "get_data()"](/v3/documentation/smart-contracts/func/docs/stdlib/#get_data)
>
> æ–‡æ¡£ä¸­çš„["new_dict()"](/v3/documentation/smart-contracts/func/docs/stdlib/#new_dict)
>
> æ–‡æ¡£ä¸­çš„ ["slice_empty?()"](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_empty)
>
> æ–‡æ¡£ä¸­çš„["load_dict()"](/v3/documentation/smart-contracts/func/docs/stdlib/#load_dict)
>
> [æ–‡æ¡£ä¸­çš„ "~"](/v3/documentation/smart-contracts/func/docs/statements#unary-operators)

### å¦‚ä½•å‘é€ç®€å•ä¿¡æ¯

æˆ‘ä»¬å‘é€å¸¦æœ‰æ³¨é‡Šçš„ TON çš„é€šå¸¸æ–¹å¼å®é™…ä¸Šæ˜¯å‘é€ä¸€æ¡ç®€å•çš„æ¶ˆæ¯ã€‚è¦æŒ‡å®šä¿¡æ¯æ­£æ–‡ä¸º "æ³¨é‡Š"ï¼Œæˆ‘ä»¬åº”å°†ä¿¡æ¯æ–‡æœ¬å‰çš„ "32 ä½ "è®¾ç½®ä¸º 0ã€‚

```func
cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(100) ;; amount of nanoTons to send
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(0, 32) ;; zero opcode - means simple transfer message with comment
    .store_slice("Hello from FunC!") ;; comment
.end_cell();
send_raw_message(msg, 3); ;; mode 3 - pay fees separately, ignore errors
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "æ¶ˆæ¯å¸ƒå±€"](/v3/documentation/smart-contracts/message-management/sending-messages)

### å¦‚ä½•ç”¨æ¥æ”¶å¸æˆ·å‘é€ä¿¡æ¯

å¦‚æœæˆ‘ä»¬éœ€è¦åœ¨ç”¨æˆ·å’Œä¸»åˆçº¦ä¹‹é—´æ‰§è¡Œä»»ä½•æ“ä½œï¼Œå³æˆ‘ä»¬éœ€è¦ä¸€ä¸ªä»£ç†åˆçº¦ï¼Œé‚£ä¹ˆä¸‹é¢çš„åˆçº¦ç¤ºä¾‹å¯¹æˆ‘ä»¬å¾ˆæœ‰ç”¨ã€‚

```func
() recv_internal (slice in_msg_body) {
    {-
        This is a simple example of a proxy-contract.
        It will expect in_msg_body to contain message mode, body and destination address to be sent to.
    -}

    int mode = in_msg_body~load_uint(8); ;; first byte will contain msg mode
    slice addr = in_msg_body~load_msg_addr(); ;; then we parse the destination address
    slice body = in_msg_body; ;; everything that is left in in_msg_body will be our new message's body

    cell msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(addr)
        .store_coins(100) ;; just for example
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
        .store_slice(body)
    .end_cell();
    send_raw_message(msg, mode);
}
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "æ¶ˆæ¯å¸ƒå±€"](/v3/documentation/smart-contracts/message-management/sending-messages)
>
> [æ–‡æ¡£ä¸­çš„"load_msg_addr() "](/v3/documentation/smart-contracts/func/docs/stdlib/#load_msg_addr)

### å¦‚ä½•å‘é€åŒ…å«å…¨éƒ¨ä½™é¢çš„ä¿¡æ¯

å¦‚æœæˆ‘ä»¬éœ€è¦å‘é€æ™ºèƒ½åˆçº¦çš„å…¨éƒ¨ä½™é¢ï¼Œé‚£ä¹ˆåœ¨è¿™ç§æƒ…å†µä¸‹ï¼Œæˆ‘ä»¬éœ€è¦ä½¿ç”¨å‘é€ "mode 128"ã€‚è¿™ç§æƒ…å†µçš„ä¸€ä¸ªä¾‹å­æ˜¯ä»£ç†åˆçº¦ï¼Œå®ƒæ¥å—ä»˜æ¬¾å¹¶è½¬å‘ç»™ä¸»åˆçº¦ã€‚

```func
cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(0) ;; we don't care about this value right now
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(0, 32) ;; zero opcode - means simple transfer message with comment
    .store_slice("Hello from FunC!") ;; comment
.end_cell();
send_raw_message(msg, 128); ;; mode = 128 is used for messages that are to carry all the remaining balance of the current smart contract
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "æ¶ˆæ¯å¸ƒå±€"](/v3/documentation/smart-contracts/message-management/sending-messages)
>
> [æ–‡æ¡£ä¸­çš„ "æ¶ˆæ¯æ¨¡å¼"](/v3/documentation/smart-contracts/func/docs/stdlib/#send_raw_message)

### å¦‚ä½•å‘é€å¸¦æœ‰é•¿æ–‡æœ¬æ³¨é‡Šçš„ä¿¡æ¯

æˆ‘ä»¬çŸ¥é“ï¼Œä¸€ä¸ª " cell "ï¼ˆ< 1023 ä½ï¼‰åªèƒ½å®¹çº³ 127 ä¸ªå­—ç¬¦ã€‚å¦‚æœæˆ‘ä»¬éœ€è¦æ›´å¤šï¼Œå°±éœ€è¦ç»„ç»‡ snake cells ã€‚

```func
{-
    If we want to send a message with really long comment, we should split the comment to several slices.
    Each slice should have <1023 bits of data (127 chars).
    Each slice should have a reference to the next one, forming a snake-like structure.
-}

cell body = begin_cell()
    .store_uint(0, 32) ;; zero opcode - simple message with comment
    .store_slice("long long long message...")
    .store_ref(begin_cell()
        .store_slice(" you can store string of almost any length here.")
        .store_ref(begin_cell()
            .store_slice(" just don't forget about the 127 chars limit for each slice")
        .end_cell())
    .end_cell())
.end_cell();

cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    ;; We use literal `a` to get valid address inside slice from string containing address 
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(100) ;; amount of nanoTons to send
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1) ;; default message headers (see sending messages page)
    .store_uint(1, 1) ;; we want to store body as a ref
    .store_ref(body)
.end_cell();
send_raw_message(msg, 3); ;; mode 3 - pay fees separately, ignore errors
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "å†…éƒ¨ä¿¡æ¯"](/v3/documentation/smart-contracts/message-management/internal-messages)

### å¦‚ä½•ä»ç‰‡æ®µä¸­åªè·å–æ•°æ®ä½ï¼ˆæ— å‚è€ƒæ–‡çŒ®ï¼‰

å¦‚æœæˆ‘ä»¬å¯¹ "ç‰‡æ–­ "ä¸­çš„ "å¼•ç”¨ "ä¸æ„Ÿå…´è¶£ï¼Œé‚£ä¹ˆæˆ‘ä»¬å¯ä»¥è·å–ä¸€ä¸ªå•ç‹¬çš„æ—¥æœŸå¹¶ä½¿ç”¨å®ƒã€‚

```func
slice s = begin_cell()
    .store_slice("Some data bits...")
    .store_ref(begin_cell().end_cell()) ;; some references
    .store_ref(begin_cell().end_cell()) ;; some references
.end_cell().begin_parse();

slice s_only_data = s.preload_bits(s.slice_bits());
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ " slice åŸè¯­"](/v3/documentation/smart-contracts/func/docs/stdlib/#slice-primitives)
>
> æ–‡æ¡£ä¸­çš„["preload_bits()"](/v3/documentation/smart-contracts/func/docs/stdlib/#preload_bits)
>
> æ–‡æ¡£ä¸­çš„["slice_bits()"](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_bits)

### å¦‚ä½•å®šä¹‰è‡ªå·±çš„ä¿®æ”¹æ–¹æ³•

ä¿®æ”¹æ–¹æ³•å…è®¸åœ¨åŒä¸€å˜é‡å†…ä¿®æ”¹æ•°æ®ã€‚è¿™å¯ä»¥ä¸å…¶ä»–ç¼–ç¨‹è¯­è¨€ä¸­çš„å¼•ç”¨ç›¸æ¯”è¾ƒã€‚

```func
(slice, (int)) load_digit (slice s) {
    int x = s~load_uint(8); ;; load 8 bits (one char) from slice
    x -= 48; ;; char '0' has code of 48, so we substract it to get the digit as a number
    return (s, (x)); ;; return our modified slice and loaded digit
}

() main () {
    slice s = "258";
    int c1 = s~load_digit();
    int c2 = s~load_digit();
    int c3 = s~load_digit();
    ;; here s is equal to "", and c1 = 2, c2 = 5, c3 = 8
}
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "ä¿®æ”¹æ–¹æ³•"](/v3/documentation/smart-contracts/func/docs/statements#modifying-methods)

### å¦‚ä½•å°†å­—ç¬¦ä¸²è½¬æ¢ä¸º int

```func
;; Unoptimized variant
int pow (int a, int n) {
    int i = 0;
    int value = a;
    while (i < n - 1) {
        a *= value;
        i += 1;
    }
    return a;
}

;; Optimized variant
(int) binpow (int n, int e) {
    if (e == 0) {
        return 1;
    }
    if (e == 1) {
        return n;
    }
    int p = binpow(n, e / 2);
    p *= p;
    if ((e % 2) == 1) {
        p *= n;
    }
    return p;
}

() main () {
    int num = binpow(2, 3);
    ~dump(num); ;; 8
}
```

### å¦‚ä½•å°† int è½¬æ¢ä¸º string

```func
slice string_number = "26052021";
int number = 0;

while (~ string_number.slice_empty?()) {
    int char = string_number~load_uint(8);
    number = (number * 10) + (char - 48); ;; we use ASCII table
}

~dump(number);
```

### å¦‚ä½•éå†å­—å…¸

```func
int n = 261119911;
builder string = begin_cell();
tuple chars = null();
do {
    int r = n~divmod(10);
    chars = cons(r + 48, chars);
} until (n == 0);
do {
    int char = chars~list_next();
    string~store_uint(char, 8);
} until (null?(chars));

slice result = string.end_cell().begin_parse();
~dump(result);
```

### å¦‚ä½•è¿­ä»£å­—å…¸

å­—å…¸åœ¨å¤„ç†å¤§é‡æ•°æ®æ—¶éå¸¸æœ‰ç”¨ã€‚æˆ‘ä»¬å¯ä»¥ä½¿ç”¨å†…ç½®æ–¹æ³• `dict_get_min?` å’Œ `dict_get_max?` åˆ†åˆ«è·å–é”®å€¼çš„æœ€å°å€¼å’Œæœ€å¤§å€¼ã€‚æ­¤å¤–ï¼Œæˆ‘ä»¬è¿˜å¯ä»¥ä½¿ç”¨ `dict_get_next?` éå†å­—å…¸ã€‚

```func
cell d = new_dict();
d~udict_set(256, 1, "value 1");
d~udict_set(256, 5, "value 2");
d~udict_set(256, 12, "value 3");

;; iterate keys from small to big
(int key, slice val, int flag) = d.udict_get_min?(256);
while (flag) {
    ;; do something with pair key->val
    
    (key, val, flag) = d.udict_get_next?(256, key);
}
```

> ğŸ’¡ Useful links
>
> [æ–‡æ¡£ä¸­çš„ "å­—å…¸åŸè¯­"](/v3/documentation/smart-contracts/func/docs/stdlib/#dictionaries-primitives)
>
> [æ–‡æ¡£ä¸­çš„ "dict_get_max?() "](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_get_max)
>
> [æ–‡æ¡£ä¸­çš„ "dict_get_min?() "](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_get_min)
>
> [æ–‡æ¡£ä¸­çš„"dict_get_next?() "](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_get_next)
>
> [æ–‡æ¡£ä¸­çš„ "dict_set() "](/v3/documentation/smart-contracts/func/docs/stdlib/#dict_set)

### å¦‚ä½•é€’å½’éå†cellæ ‘

```func
cell names = new_dict();
names~udict_set(256, 27, "Alice");
names~udict_set(256, 25, "Bob");

names~udict_delete?(256, 27);

(slice val, int key) = names.udict_get?(256, 27);
~dump(val); ;; null() -> means that key was not found in a dictionary
```

### å¦‚ä½•é€’å½’éå† cell æ ‘

æˆ‘ä»¬çŸ¥é“ï¼Œä¸€ä¸ª " cell  "æœ€å¤šå¯ä»¥å­˜å‚¨ 1023 ä½æ•°æ®å’Œ 4 ä¸ªå¼•ç”¨ã€‚ä¸ºäº†ç»•è¿‡è¿™ä¸€é™åˆ¶ï¼Œæˆ‘ä»¬å¯ä»¥ä½¿ç”¨ cell æ ‘ï¼Œä½†è¦åšåˆ°è¿™ä¸€ç‚¹ï¼Œæˆ‘ä»¬éœ€è¦èƒ½å¤Ÿéå† cell æ ‘ï¼Œä»¥ä¾¿è¿›è¡Œé€‚å½“çš„æ•°æ®å¤„ç†ã€‚

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() main () {
    ;; just some cell for example
    cell c = begin_cell()
        .store_uint(1, 16)
        .store_ref(begin_cell()
            .store_uint(2, 16)
        .end_cell())
        .store_ref(begin_cell()
            .store_uint(3, 16)
            .store_ref(begin_cell()
                .store_uint(4, 16)
            .end_cell())
            .store_ref(begin_cell()
                .store_uint(5, 16)
            .end_cell())
        .end_cell())
    .end_cell();

    ;; creating tuple with no data, which plays the role of stack
    tuple stack = null();
    ;; bring the main cell into the stack to process it in the loop
    stack~push_back(c);
    ;; do it until stack is not null
    while (~ stack.is_null()) {
        ;; get the cell from the stack and convert it to a slice to be able to process it
        slice s = stack~pop_back().begin_parse();

        ;; do something with s data

        ;; if the current slice has any refs, add them to stack
        repeat (s.slice_refs()) {
            stack~push_back(s~load_ref());
        }
    }
}
```

> [æ–‡æ¡£ä¸­çš„â€œnull()â€](/develop/func/stdlib/#null)
>
> [æ–‡æ¡£ä¸­çš„â€œslice_refs()â€](/develop/func/stdlib/#slice_refs)
>
> [æ–‡æ¡£ä¸­çš„ "null() "](/v3/documentation/smart-contracts/func/docs/stdlib/#null)
>
> [æ–‡æ¡£ä¸­çš„ "slice_refs()"](/v3/documentation/smart-contracts/func/docs/stdlib/#slice_refs)

### å¦‚ä½•éå† Lisp ç±»å‹åˆ—è¡¨

æ•°æ®ç±»å‹ tuple æœ€å¤šå¯ä»¥å®¹çº³ 255 ä¸ªå€¼ã€‚å¦‚æœè¿™è¿˜ä¸å¤Ÿï¼Œæˆ‘ä»¬åº”è¯¥ä½¿ç”¨ Lisp ç±»å‹çš„åˆ—è¡¨ã€‚æˆ‘ä»¬å¯ä»¥å°†ä¸€ä¸ª tuple æ”¾å…¥å¦ä¸€ä¸ª tuple ä¸­ï¼Œä»è€Œç»•è¿‡é™åˆ¶ã€‚

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() main () {
    ;; some example list
    tuple l = null();
    l~push_back(1);
    l~push_back(2);
    l~push_back(3);

    ;; iterating through elements
    ;; note that this iteration is in reversed order
    while (~ l.is_null()) {
        var x = l~pop_back();

        ;; do something with x
    }
}
```

> ğŸ’¡ æœ‰ç”¨çš„é“¾æ¥
>
> [æ–‡æ¡£ä¸­çš„â€œLispé£æ ¼åˆ—è¡¨â€](/develop/func/stdlib/#lisp-style-lists)
>
> [æ–‡æ¡£ä¸­çš„â€œnull()â€](/develop/func/stdlib/#null)

### å¦‚ä½•å‘é€éƒ¨ç½²æ¶ˆæ¯ï¼ˆä»…ä½¿ç”¨ stateInit æˆ–ä½¿ç”¨ stateInit å’Œ bodyï¼‰

```func
() deploy_with_stateinit(cell message_header, cell state_init) impure {
  var msg = begin_cell()
    .store_slice(begin_parse(msg_header))
    .store_uint(2 + 1, 2) ;; init:(Maybe (Either StateInit ^StateInit))
    .store_uint(0, 1) ;; body:(Either X ^X)
    .store_ref(state_init)
    .end_cell();

  ;; mode 64 - carry the remaining value in the new message
  send_raw_message(msg, 64); 
}

() deploy_with_stateinit_body(cell message_header, cell state_init, cell body) impure {
  var msg = begin_cell()
    .store_slice(begin_parse(msg_header))
    .store_uint(2 + 1, 2) ;; init:(Maybe (Either StateInit ^StateInit))
    .store_uint(1, 1) ;; body:(Either X ^X)
    .store_ref(state_init)
    .store_ref(body)
    .end_cell();

  ;; mode 64 - carry the remaining value in the new message
  send_raw_message(msg, 64); 
}
```

### å¦‚ä½•æ„å»º stateInit cell

```func
() build_stateinit(cell init_code, cell init_data) {
  var state_init = begin_cell()
    .store_uint(0, 1) ;; split_depth:(Maybe (## 5))
    .store_uint(0, 1) ;; special:(Maybe TickTock)
    .store_uint(1, 1) ;; (Maybe ^Cell)
    .store_uint(1, 1) ;; (Maybe ^Cell)
    .store_uint(0, 1) ;; (HashmapE 256 SimpleLib)
    .store_ref(init_code)
    .store_ref(init_data)
    .end_cell();
}
```

### å¦‚ä½•è®¡ç®—åˆçº¦åœ°å€ï¼ˆä½¿ç”¨ stateInitï¼‰

```func
() calc_address(cell state_init) {
  var future_address = begin_cell() 
    .store_uint(2, 2) ;; addr_std$10
    .store_uint(0, 1) ;; anycast:(Maybe Anycast)
    .store_uint(0, 8) ;; workchain_id:int8
    .store_uint(cell_hash(state_init), 256) ;; address:bits256
    .end_cell();
}
```

### å¦‚ä½•æ›´æ–°æ™ºèƒ½åˆçº¦é€»è¾‘

ä¸‹é¢æ˜¯ä¸€ä¸ªç®€å•çš„ `Ğ¡ounterV1` æ™ºèƒ½åˆçº¦ï¼Œå®ƒå…·æœ‰é€’å¢è®¡æ•°å™¨å’Œæ›´æ–°æ™ºèƒ½åˆçº¦é€»è¾‘çš„åŠŸèƒ½ã€‚

```func
() recv_internal (slice in_msg_body) {
    int op = in_msg_body~load_uint(32);
    
    if (op == op::increase) {
        int increase_by = in_msg_body~load_uint(32);
        ctx_counter += increase_by;
        save_data();
        return ();
    }

    if (op == op::upgrade) {
        cell code = in_msg_body~load_ref();
        set_code(code);
        return ();
    }
}
```

åœ¨æ“ä½œæ™ºèƒ½åˆçº¦åï¼Œæ‚¨å‘ç°ç¼ºå°‘äº†å‡è¡¨åŠŸèƒ½ã€‚æ‚¨å¿…é¡»å¤åˆ¶æ™ºèƒ½åˆçº¦ "CounterV1 "çš„ä»£ç ï¼Œå¹¶åœ¨ "å¢åŠ  "å‡½æ•°æ—è¾¹æ·»åŠ ä¸€ä¸ªæ–°çš„ "å‡å°‘ "å‡½æ•°ã€‚ç°åœ¨æ‚¨çš„ä»£ç å¦‚ä¸‹

```func
() recv_internal (slice in_msg_body) {
    int op = in_msg_body~load_uint(32);
    
    if (op == op::increase) {
        int increase_by = in_msg_body~load_uint(32);
        ctx_counter += increase_by;
        save_data();
        return ();
    }

    if (op == op::decrease) {
        int decrease_by = in_msg_body~load_uint(32);
        ctx_counter -= increase_by;
        save_data();
        return ();
    }

    if (op == op::upgrade) {
        cell code = in_msg_body~load_ref();
        set_code(code);
        return ();
    }
}
```

ä¸€æ—¦æ™ºèƒ½åˆçº¦ "CounterV2 "å‡†å¤‡å°±ç»ªï¼Œä½ å¿…é¡»å°†å…¶ç¼–è¯‘åˆ°é“¾å¤–çš„ "cell "ä¸­ï¼Œå¹¶å‘ "CounterV1 "æ™ºèƒ½åˆçº¦å‘é€å‡çº§æ¶ˆæ¯ã€‚

```javascript
await contractV1.sendUpgrade(provider.sender(), {
    code: await compile('ContractV2'),
    value: toNano('0.05'),
});
```

> ğŸ’¡ Useful links
>
> [æ˜¯å¦å¯ä»¥å°†ä»£ç é‡æ–°éƒ¨ç½²åˆ°ç°æœ‰åœ°å€ï¼Œè¿˜æ˜¯å¿…é¡»å°†å…¶ä½œä¸ºæ–°åˆçº¦éƒ¨ç½²ï¼Ÿ](/v3/documentation/faq#is-it-possible-to-re-deploy-code-to-an-existing-address-or-does-it-have-to-be-deployed-as-a-new-contract)
>
> [æ–‡æ¡£ä¸­çš„ "set_code()"](/v3/documentation/smart-contracts/func/docs/stdlib#set_code)
